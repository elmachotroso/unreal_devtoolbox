// Copyright Epic Games, Inc. All Rights Reserved.

/*=============================================================================
	LandscapeVertexFactory.usf: Landscape vertex factory.
=============================================================================*/

#include "VertexFactoryCommon.ush"

#define VERTEX_FACTORY_MODIFIES_INTERPOLATION 1
#define VERTEX_FACTORY_HEIGHTFIELD 1

// Set by FLightMapTexturePolicy
#include "/Engine/Generated/UniformBuffers/PrecomputedLightingBuffer.ush"

#if LANDSCAPE_XYOFFSET
	#define XYOFFSET_SCALE (1.0f/256.f)
#endif

#if (ES3_1_PROFILE)
/* Offset for UV localization for large UV values. */
float2 TexCoordOffset;
#endif

/* Return index into buffers for this section instance. */
int GetComponentLinearIndex()
{
#if FIXED_GRID
	// Fixed grid doesn't use LandscapeContinuousLODParameters buffers
	return 0;
#else
	return (LandscapeParameters.ComponentBaseY - LandscapeContinuousLODParameters.Min.y) * LandscapeContinuousLODParameters.Size.x + (LandscapeParameters.ComponentBaseX - LandscapeContinuousLODParameters.Min.x);
#endif
}

float GetViewLODData(uint ComponentIndex)
{
#if FIXED_GRID
	// Fixed grid doesn't use LandscapeContinuousLODParameters buffers
	return 0;
#else
	const int AbsoluteSectionLODIndex = View.LandscapeIndirection[LandscapeContinuousLODParameters.LandscapeIndex] + ComponentIndex;
	return View.OverrideLandscapeLOD >= 0.0f ? View.OverrideLandscapeLOD : View.LandscapePerComponentData[AbsoluteSectionLODIndex];
#endif
}

/* Get current Lod for this section instance. */
float GetSectionLod(uint ComponentIndex)
{
#if FIXED_GRID
	// Fixed grid has fixed Lod
	return LandscapeFixedGrid.LodValues.x;
#else
	return min(GetViewLODData(ComponentIndex) + View.FarShadowStaticMeshLODBias, LandscapeParameters.LastLOD);
#endif
}

/**
 * x = Current mesh LOD
 * y = unused
 * z = SectionSizeQuads in current mesh LOD
 * w = 1/SectionSizeQuads in current mesh LOD
 **/
float4 GetLodValues(uint ComponentIndex)
{
#if FIXED_GRID 
	// Fixed grid has fixed Lod
	return LandscapeFixedGrid.LodValues;
#elif FEATURE_LEVEL < FEATURE_LEVEL_SM4
	// On mobile we always use the Lod 0 mesh data
	return float4(0, 0, LandscapeParameters.SubsectionSizeVertsLayerUVPan.xy);
#else
	// Get Lod for the current section and infer the values
	uint Lod = (uint)floor(GetSectionLod(ComponentIndex));
	float LodSubSectionSizeQuads = (float)((LandscapeParameters.SubsectionSizeVerts >> Lod) - 1);
	return float4(Lod, 0, LodSubSectionSizeQuads, rcp(LodSubSectionSizeQuads));
#endif
}

/**
 * x = Heightmap texture LOD difference from current LOD to highest LOD
 * y = todo: XYOffset texture LOD difference from current LOD to highest LOD
 */
float2 GetLodBias(uint ComponentIndex)
{
#if FIXED_GRID || FEATURE_LEVEL < FEATURE_LEVEL_SM4
	// Fixed grid and mobile don't use LodBias
	return 0;
#else
	float LodBias = LandscapeContinuousLODParameters.SectionLODBias[ComponentIndex];
	return float2(LodBias, 0.f);
#endif
}

/** 
 * Get current Lods for neighbour section instances. 
 *       -Y
 *    - - 0 - -
 *    |       |
 * -X 1   P   2 +X
 *    |       |
 *    - - 3 - -
 *       +Y
 */
float4 GetNeighborSectionLod(uint ComponentIndex)
{
#if FIXED_GRID
	// Fixed grid has fixed Lod
	return LandscapeFixedGrid.LodValues.xxxx;
#else
	int2 NeighborOffset[4];
    NeighborOffset[0] = int2(0, -1);
    NeighborOffset[1] = int2(-1, 0);
    NeighborOffset[2] = int2(1, 0);
    NeighborOffset[3] = int2(0, 1);
	float NeighborLod[4];
	for (int i = 0; i < 4; i++)
	{
		int2 NeighborPos = NeighborOffset[i] + int2(LandscapeParameters.ComponentBaseX, LandscapeParameters.ComponentBaseY);
		NeighborPos = max(LandscapeContinuousLODParameters.Min                                                     , NeighborPos);
		NeighborPos = min(LandscapeContinuousLODParameters.Min + LandscapeContinuousLODParameters.Size - int2(1, 1), NeighborPos);

		uint NeighborComponentIndex = (NeighborPos.y - LandscapeContinuousLODParameters.Min.y) * LandscapeContinuousLODParameters.Size.x + (NeighborPos.x - LandscapeContinuousLODParameters.Min.x);
		float SectionLOD = GetViewLODData(NeighborComponentIndex);

		NeighborLod[i] = max(SectionLOD, GetSectionLod(ComponentIndex));
		NeighborLod[i] = View.OverrideLandscapeLOD >= 0.0f ? View.OverrideLandscapeLOD : NeighborLod[i] + View.FarShadowStaticMeshLODBias;
		NeighborLod[i] = min(NeighborLod[i], LandscapeParameters.LastLOD);
	}
	return float4(NeighborLod[0], NeighborLod[1], NeighborLod[2], NeighborLod[3]);
#endif
}

/** Calculate fractional render LOD for a subsection. */
float CalcLOD(uint ComponentIndex, float2 xyLocalToSubsection, float2 Subsection)
{
#if FIXED_GRID
	// Fixed grid has fixed Lod
	return LandscapeFixedGrid.LodValues.x;
#else
	float2 xy = (xyLocalToSubsection + Subsection) / LandscapeParameters.NumSubsections;
	// LOD value based on Barycentric coordinate
	float4 L0 = float4(xy.y, xy.x, (1 - xy.x), (1 - xy.y)) * 2;

	float4 LODCalculated4 = L0 * GetSectionLod(ComponentIndex) + (float4(1, 1, 1, 1) - L0) * GetNeighborSectionLod(ComponentIndex);

	// Find which quadrant of the subsection we're in - top, left, right or bottom.
	// 0 --------- 1 X  1 ----- 2  1          1 
	//   | \ 0 / |        \   /    | \      / | 
	//   | 1 + 2 |          0      |  0    0  |      0       
	//   | / 3 \ |                 | /      \ |    /   \     
	// 2 --------- 3               2          2  1 ----- 2   
	float LODCalculated = ((xy.x + xy.y) > 1) ?
		(xy.x < xy.y ? LODCalculated4.w : LODCalculated4.z) :
		(xy.x < xy.y ? LODCalculated4.y : LODCalculated4.x);

	return LODCalculated;
#endif
}

struct FVertexFactoryInput
{
#if FEATURE_LEVEL >= FEATURE_LEVEL_SM4
	float4 Position: ATTRIBUTE0;

	// Dynamic instancing related attributes with InstanceIdOffset : ATTRIBUTE1
	VF_GPUSCENE_DECLARE_INPUT_BLOCK(1)
#else
	float4 PackedPosition: ATTRIBUTE0; 
	float4 LODHeights0 : ATTRIBUTE1;  
	float4 LODHeights1 : ATTRIBUTE2;
#endif
};

// RHI_RAYTRACING
#if RAYHITGROUPSHADER
FVertexFactoryInput LoadVertexFactoryInputForHGS(uint TriangleIndex, int VertexIndex)
{
	FVertexFactoryInput Input;

	float4 LodValues = GetLodValues(GetComponentLinearIndex());
	uint SubsectionSizeQuadsCurrentLOD = LodValues.z;
	uint SubsectionSizeVertsCurrentLOD = LodValues.z + 1;

	#if 1
		FTriangleBaseAttributes Tri = LoadTriangleBaseAttributes(TriangleIndex);
		uint VertexId = Tri.Indices[VertexIndex];
	#else
		uint QuadId = TriangleIndex / 2;
		uint QuadX = QuadId % SubsectionSizeQuadsCurrentLOD;
		uint QuadY = QuadId / SubsectionSizeQuadsCurrentLOD;
		uint i00 = (QuadX + 0) + (QuadY + 0) * SubsectionSizeVertsCurrentLOD;
		uint i10 = (QuadX + 1) + (QuadY + 0) * SubsectionSizeVertsCurrentLOD;
		uint i11 = (QuadX + 1) + (QuadY + 1) * SubsectionSizeVertsCurrentLOD;
		uint i01 = (QuadX + 0) + (QuadY + 1) * SubsectionSizeVertsCurrentLOD;
		uint Indices[6] = {i00, i11, i10, i00, i01, i10};
		uint VertexId = Indices[TriangleIndex % 2 * 3 + VertexIndex];
	#endif
#if VF_USE_PRIMITIVE_SCENE_DATA
	// Note: GetInstanceUserData() stores the GPU-Scene primitive ID
	Input.InstanceIdOffset = GetInstanceUserData() | VF_TREAT_INSTANCE_ID_OFFSET_AS_PRIMITIVE_ID_FLAG;
	Input.DrawInstanceId = 0;
#endif // VF_USE_PRIMITIVE_SCENE_DATA


	Input.Position.x = VertexId % SubsectionSizeVertsCurrentLOD;
	Input.Position.y = VertexId / SubsectionSizeVertsCurrentLOD;
	Input.Position.z = LandscapeMVF.SubXY.x;
	Input.Position.w = LandscapeMVF.SubXY.y;

	return Input;
}
#endif

#if COMPUTESHADER
FVertexFactoryInput LoadVertexFactoryInputForDynamicUpdate(uint TriangleIndex, int VertexIndex, uint PrimitiveId)
{
	FVertexFactoryInput Input = (FVertexFactoryInput)0;

	float4 LodValues = GetLodValues(GetComponentLinearIndex());
	uint SubsectionSizeQuadsCurrentLOD = LodValues.z;
	uint SubsectionSizeVertsCurrentLOD = LodValues.z + 1;

	uint VertexId = TriangleIndex * 3 + VertexIndex;

	Input.Position.x = VertexId % SubsectionSizeVertsCurrentLOD;
	Input.Position.y = VertexId / SubsectionSizeVertsCurrentLOD;
	Input.Position.z = LandscapeMVF.SubXY.x;
	Input.Position.w = LandscapeMVF.SubXY.y;

#if VF_USE_PRIMITIVE_SCENE_DATA
	// GPUCULL_TODO: Makes use of DynamicDraw workaround, should instead refactor VF interfaces and provide the instance ID data directly.
	Input.InstanceIdOffset = PrimitiveId | VF_TREAT_INSTANCE_ID_OFFSET_AS_PRIMITIVE_ID_FLAG;
	Input.DrawInstanceId = 0;
#endif // VF_USE_PRIMITIVE_SCENE_DATA

	return Input;
}

uint GetNumRayTracingDynamicMeshVerticesIndirect()
{
	return 0;
}
#endif

struct FVertexFactoryInterpolantsVSToPS
{
	float2	LayerTexCoord			: TEXCOORD0; // xy == texcoord
#if (ES3_1_PROFILE)
	float2	WeightMapTexCoord		: TEXCOORD1;
	float4	TransformedTexCoord0	: TEXCOORD2;
	float4  TransformedTexCoord1	: TEXCOORD3;
#else
	float4	WeightHeightMapTexCoord	: TEXCOORD1;
	float4	TransformedTexCoords	: TEXCOORD2;
#endif
#if NEEDS_LIGHTMAP_COORDINATE
#if (ES3_1_PROFILE)
	float2	LightMapCoordinate[2]	: TEXCOORD4;
	float2	ShadowMapCoordinate		: TEXCOORD6;
#else
	float4	LightMapCoordinate		: TEXCOORD3;
#endif
#endif

#if INSTANCED_STEREO
	nointerpolation uint EyeIndex : PACKED_EYE_INDEX;
#endif

#if VF_USE_PRIMITIVE_SCENE_DATA
	nointerpolation uint PrimitiveId : PRIMITIVE_ID;
#endif
};

struct FLandscapeTexCoords
{
	float2	LayerTexCoord : TEXCOORD0; // xy == texcoord
	float2	WeightMapTexCoord;
#if !(ES3_1_PROFILE)
	float2	HeightMapTexCoord;
#endif
#if NEEDS_LIGHTMAP_COORDINATE
	float2	LightMapCoordinate;
#endif
};

struct FVertexFactoryRayTracingInterpolants
{
	FVertexFactoryInterpolantsVSToPS InterpolantsVSToPS;
	
#if NEEDS_VERTEX_FACTORY_INTERPOLATION
	// First row of the tangent to world matrix
	float3	TangentToWorld0	: VS_To_DS_TangentToWorld0;
	// Last row of the tangent to world matrix in xyz
	float4	TangentToWorld2	: VS_To_DS_TangentToWorld2;
	// LOD of the vertex, used for fading out tessellation
	float CalculatedLOD : VS_To_DS_CalculatedLOD;
#endif
};

#if NEEDS_VERTEX_FACTORY_INTERPOLATION
float2 VertexFactoryGetRayTracingTextureCoordinate( FVertexFactoryRayTracingInterpolants Interpolants )
{
#if (ES3_1_PROFILE)
	return Interpolants.InterpolantsVSToPS.WeightMapTexCoord.xy;
#else
	return Interpolants.InterpolantsVSToPS.WeightHeightMapTexCoord.zw;
#endif
}
#endif // #if NEEDS_VERTEX_FACTORY_INTERPOLATION

FVertexFactoryInterpolantsVSToPS VertexFactoryAssignInterpolants(FVertexFactoryRayTracingInterpolants Input)
{
	return Input.InterpolantsVSToPS;
}

struct FVertexFactoryIntermediates
{
	float4 InputPosition;
	float3 LocalPosition;
	float3 WorldNormal;
	uint ComponentIndex;
	float4 LodValues;
	float2 LodBias;
	/** Cached primitive and instance data */
	FSceneDataIntermediates SceneData;
};

FPrimitiveSceneData GetPrimitiveData(FVertexFactoryIntermediates Intermediates)
{
	return Intermediates.SceneData.Primitive;
}

FInstanceSceneData GetInstanceData(FVertexFactoryIntermediates Intermediates)
{
	return Intermediates.SceneData.InstanceData;
}

float3 GetLocalPosition(FVertexFactoryIntermediates Intermediates)
{
	return INVARIANT(Intermediates.LocalPosition+float3(Intermediates.InputPosition.zw * LandscapeParameters.SubsectionOffsetParams.ww,0));
}

float4 VertexFactoryGetWorldPosition(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates)
{
	FLWCMatrix LocalToWorld = GetInstanceData(Intermediates).LocalToWorld;
	return INVARIANT(TransformLocalToTranslatedWorld(GetLocalPosition(Intermediates), LocalToWorld));
}

float3 VertexFactoryGetWorldNormal(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates)
{
	return Intermediates.WorldNormal;
}

float4 VertexFactoryGetPreviousWorldPosition(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates)
{
	// Note we still use LocalToWorld. Landscape's transform never changes.
	float3 LocalPosition = GetLocalPosition(Intermediates);
	FLWCMatrix LocalToWorld = GetInstanceData(Intermediates).LocalToWorld;

	FLWCVector3 WorldPosition = LWCInvariantMultiply(LocalPosition, LocalToWorld);
	return float4(LWCToFloat(LWCAdd(WorldPosition, ResolvedView.PrevPreViewTranslation)), 1.0f);
}

/** Calculate the texture coordinates generated by Landscape */
FLandscapeTexCoords GetLandscapeTexCoords(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates)
{
	FLandscapeTexCoords Result;

	Result.LayerTexCoord.xy = Intermediates.LocalPosition.xy + LandscapeParameters.SubsectionSizeVertsLayerUVPan.zw + Intermediates.InputPosition.zw * LandscapeParameters.SubsectionOffsetParams.ww;

	Result.WeightMapTexCoord = Intermediates.LocalPosition.xy * LandscapeParameters.WeightmapUVScaleBias.xy + LandscapeParameters.WeightmapUVScaleBias.zw + Intermediates.InputPosition.zw * LandscapeParameters.SubsectionOffsetParams.zz;

#if !(ES3_1_PROFILE)
	Result.HeightMapTexCoord = Intermediates.LocalPosition.xy * LandscapeParameters.HeightmapUVScaleBias.xy + LandscapeParameters.HeightmapUVScaleBias.zw + 0.5*LandscapeParameters.HeightmapUVScaleBias.xy + Intermediates.InputPosition.zw * LandscapeParameters.SubsectionOffsetParams.xy;
#endif

#if NEEDS_LIGHTMAP_COORDINATE
	Result.LightMapCoordinate = (Intermediates.LocalPosition.xy * LandscapeParameters.LandscapeLightmapScaleBias.xy + LandscapeParameters.LandscapeLightmapScaleBias.wz + Intermediates.InputPosition.zw * LandscapeParameters.LightmapSubsectionOffsetParams.xy);
#endif
	
	return Result;
}

float3x3 CalcTangentBasisFromWorldNormal(float3 Normal)
{
	float3 LocalTangentX = normalize(float3(Normal.z, 0, -Normal.x));
	float3 LocalTangentY = cross(Normal, LocalTangentX);

	float3x3 LocalToTangent = float3x3(LocalTangentX,LocalTangentY,Normal);

	return LocalToTangent;
}

/** Lookup per-pixel tangent basis from heightmap texture */
float3x3 VertexFactoryGetPerPixelTangentBasis(FVertexFactoryInterpolantsVSToPS Interpolants)
{
	float3x3 Result;
#if PIXELSHADER || RAYHITGROUPSHADER || COMPUTESHADER
#if FEATURE_LEVEL >= FEATURE_LEVEL_SM4
	float4 SampleValue = Texture2DSample(LandscapeParameters.NormalmapTexture, LandscapeParameters.NormalmapTextureSampler, Interpolants.WeightHeightMapTexCoord.zw);
#else
	float4 SampleValue = Texture2DSample(LandscapeParameters.NormalmapTexture, LandscapeParameters.NormalmapTextureSampler, Interpolants.WeightMapTexCoord);
#endif
#if WEBGL
	float2 SampleNormal = float2(SampleValue.b, SampleValue.a) * float2(2.0,2.0) - float2(1.0,1.0);
#else
	float2 SampleNormal = float2(SampleValue.b, SampleValue.a) * float2(2.0,2.0) - float2(1.0,1.0);
#endif
	float3 WorldNormal = float3( SampleNormal, sqrt(max(1.0-dot(SampleNormal,SampleNormal),0.0)) );

	Result = CalcTangentBasisFromWorldNormal(WorldNormal);
#endif
	return Result;
}

/** Converts from vertex factory specific interpolants to a FMaterialPixelParameters, which is used by material inputs. */
FMaterialPixelParameters GetMaterialPixelParameters(FVertexFactoryInterpolantsVSToPS Interpolants, float4 SvPosition)
{
	// GetMaterialPixelParameters is responsible for fully initializing the result
	FMaterialPixelParameters Result = MakeInitializedMaterialPixelParameters();

#if NEEDS_LIGHTMAP_COORDINATE
	#if (ES3_1_PROFILE)
		// Not supported in pixel shader
		float2 LightmapUVs = float2(0, 0);
	#else
		float2 LightmapUVs = Interpolants.LightMapCoordinate.xy;
	#endif
#else
	float2 LightmapUVs = float2(0, 0);
#endif

#if NUM_MATERIAL_TEXCOORDS     // XY layer
	Result.TexCoords[0] = Interpolants.LayerTexCoord.xy;
#if NUM_MATERIAL_TEXCOORDS > 1 // VS calcualted TexCoord 1, default is XZ layer
	#if (ES3_1_PROFILE)
		Result.TexCoords[1] = Interpolants.TransformedTexCoord0.xy;
	#else
		Result.TexCoords[1] = Interpolants.TransformedTexCoords.xy;
	#endif
#if NUM_MATERIAL_TEXCOORDS > 2 // VS calcualted TexCoord 2, default is YZ layer
	#if (ES3_1_PROFILE)
		Result.TexCoords[2] = Interpolants.TransformedTexCoord1.xy;
	#else
		Result.TexCoords[2] = Interpolants.TransformedTexCoords.zw;
	#endif
#if NUM_MATERIAL_TEXCOORDS > 3 // Weightmap
	#if (ES3_1_PROFILE)
		Result.TexCoords[3] = Interpolants.WeightMapTexCoord;
	#else
		Result.TexCoords[3] = Interpolants.WeightHeightMapTexCoord.xy;
	#endif
#if NUM_MATERIAL_TEXCOORDS > 4 // Lightmap
	#if NEEDS_LIGHTMAP_COORDINATE
		Result.TexCoords[4] = LightmapUVs;
	#else
		Result.TexCoords[4] = float2(0,0);
	#endif
#if NUM_MATERIAL_TEXCOORDS > 5 // Height map
	#if (ES3_1_PROFILE)
		Result.TexCoords[5] = float2(0,0); // Height map UV not supported
	#else
		Result.TexCoords[5] = Interpolants.WeightHeightMapTexCoord.zw;
	#endif
#if NUM_MATERIAL_TEXCOORDS > 6
	for (uint CoordinateIndex = 6; CoordinateIndex < NUM_MATERIAL_TEXCOORDS; CoordinateIndex++)
	{
		Result.TexCoords[CoordinateIndex] = float2(0,0);
	}
#endif // 6
#endif // 5
#endif // 4
#endif // 3
#endif // 2
#endif // 1
#endif // 0

	// Calculate LocalToTangent directly from normal map texture.
	float3x3 TangentToLocal = VertexFactoryGetPerPixelTangentBasis(Interpolants);
	Result.TangentToWorld = mul(TangentToLocal, (float3x3)LandscapeParameters.LocalToWorldNoScaling);
	Result.UnMirrored = 1;

	Result.VertexColor = 1;

#if LIGHTMAP_UV_ACCESS
	Result.LightmapUVs = LightmapUVs;
#endif

#if (ES3_1_PROFILE) && PIXELSHADER
	Result.LayerWeights.xy = Texture2DSample(LandscapeParameters.NormalmapTexture, LandscapeParameters.NormalmapTextureSampler, Interpolants.WeightMapTexCoord).rg;
	Result.LayerWeights.z = saturate(LandscapeParameters.BlendableLayerMask.z - dot(Result.LayerWeights.xy, LandscapeParameters.BlendableLayerMask.xy));
	Result.LayerWeights.w = 0; // Total weight need to be 1
#endif

	Result.TwoSidedSign = 1;

#if VF_USE_PRIMITIVE_SCENE_DATA
	Result.PrimitiveId = Interpolants.PrimitiveId;
#endif

	return Result;
}

/** Converts from vertex factory specific input to a FMaterialVertexParameters, which is used by vertex shader material inputs. */
FMaterialVertexParameters GetMaterialVertexParameters(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, float3 WorldPosition, float3x3 TangentToLocal)
{
	FMaterialVertexParameters Result = (FMaterialVertexParameters)0;
	Result.SceneData = Intermediates.SceneData;
	Result.WorldPosition = WorldPosition;
	Result.VertexColor = float4(1,1,1,1);
	Result.TangentToWorld = mul(TangentToLocal, (float3x3)LandscapeParameters.LocalToWorldNoScaling);
	Result.PreSkinnedPosition = Intermediates.LocalPosition.xyz;
	Result.PreSkinnedNormal = TangentToLocal[2].xyz;

	// Assumes no instancing and landscape's transform never change
	Result.PrevFrameLocalToWorld = GetInstanceData(Intermediates).LocalToWorld;

	FLandscapeTexCoords LandscapeTexCoords = GetLandscapeTexCoords(Input, Intermediates);

#if NUM_MATERIAL_TEXCOORDS_VERTEX     // XY layer
	Result.TexCoords[0] = LandscapeTexCoords.LayerTexCoord.xy;
	#if (ES3_1_PROFILE)
		Result.TexCoordOffset = TexCoordOffset;
	#endif
#if NUM_MATERIAL_TEXCOORDS_VERTEX > 1 // XZ layer
	Result.TexCoords[1] = float2(LandscapeTexCoords.LayerTexCoord.x, Intermediates.LocalPosition.z);
#if NUM_MATERIAL_TEXCOORDS_VERTEX > 2 // YZ layer
	Result.TexCoords[2] = float2(LandscapeTexCoords.LayerTexCoord.y, Intermediates.LocalPosition.z);
#if NUM_MATERIAL_TEXCOORDS_VERTEX > 3 // Weightmap
	Result.TexCoords[3] = LandscapeTexCoords.WeightMapTexCoord;
#if NUM_MATERIAL_TEXCOORDS_VERTEX > 4 // Lightmap
	#if NEEDS_LIGHTMAP_COORDINATE
		Result.TexCoords[4] = LandscapeTexCoords.LightMapCoordinate.xy;
	#else
		Result.TexCoords[4] = float2(0,0);
	#endif
#if NUM_MATERIAL_TEXCOORDS_VERTEX > 5 // Height map
	#if (ES3_1_PROFILE)
		Result.TexCoords[5] = float2(0,0); // Height map UV not supported
	#else
		Result.TexCoords[5] = LandscapeTexCoords.HeightMapTexCoord;
	#endif
#if NUM_MATERIAL_TEXCOORDS_VERTEX > 6
	UNROLL
	for (uint CoordinateIndex = 6; CoordinateIndex < NUM_MATERIAL_TEXCOORDS_VERTEX; CoordinateIndex++)
	{
		Result.TexCoords[CoordinateIndex] = float2(0,0);
	}
#endif // 6
#endif // 5
#endif // 4
#endif // 3
#endif // 2
#endif // 1
#endif // 0

	return Result;
}

#if NEEDS_LIGHTMAP_COORDINATE
void GetLightMapCoordinates(FVertexFactoryInterpolantsVSToPS Interpolants, out float2 LightmapUV0, out float2 LightmapUV1, out uint LightmapDataIndex)
{
#if (ES3_1_PROFILE)
	LightmapUV0 = Interpolants.LightMapCoordinate[0].xy;
	LightmapUV1 = Interpolants.LightMapCoordinate[1].xy;
#else
	LightmapUV0 = Interpolants.LightMapCoordinate.xy * float2( 1, 0.5 );
	LightmapUV1 = LightmapUV0 + float2( 0, 0.5 );
#endif

#if VF_USE_PRIMITIVE_SCENE_DATA
	LightmapDataIndex = GetPrimitiveData(Interpolants.PrimitiveId).LightmapDataIndex;
#else
	LightmapDataIndex = 0;
#endif
}

void GetShadowMapCoordinate(FVertexFactoryInterpolantsVSToPS Interpolants, out float2 ShadowMapCoordinate, out uint LightmapDataIndex)
{
#if (ES3_1_PROFILE)
	ShadowMapCoordinate = Interpolants.ShadowMapCoordinate.xy;
#else
	ShadowMapCoordinate = Interpolants.LightMapCoordinate.zw;
#endif
	
#if VF_USE_PRIMITIVE_SCENE_DATA
	LightmapDataIndex = GetPrimitiveData(Interpolants.PrimitiveId).LightmapDataIndex;
#else
	LightmapDataIndex = 0;
#endif

}
#endif

FVertexFactoryIntermediates GetVertexFactoryIntermediates(FVertexFactoryInput Input)
{
	FVertexFactoryIntermediates Intermediates;
	Intermediates.SceneData = VF_GPUSCENE_GET_INTERMEDIATES(Input);
	Intermediates.ComponentIndex = GetComponentLinearIndex();
	Intermediates.LodValues = GetLodValues(Intermediates.ComponentIndex);
	Intermediates.LodBias = GetLodBias(Intermediates.ComponentIndex);

#if FEATURE_LEVEL >= FEATURE_LEVEL_SM4
	Intermediates.InputPosition = Input.Position;
#else
	Intermediates.InputPosition.xy = Input.PackedPosition.xy * 255.f;

	uint PackedExtraData = (uint)(Input.PackedPosition.z * 255.f);
	float SubX = (float)((PackedExtraData >> 1) & 1);
	float SubY = (float)((PackedExtraData >> 0) & 1);
	Intermediates.InputPosition.zw = float2(SubX, SubY);
	float HoleMaxLod = (float)(PackedExtraData >> 5);
	float HoleVertexLod = (float)((PackedExtraData >> 2) & 7);
#endif

	float2 xy = Intermediates.InputPosition.xy * Intermediates.LodValues.w;

	float LODCalculated = CalcLOD(Intermediates.ComponentIndex, xy, Intermediates.InputPosition.zw);
	float LodValue = floor(LODCalculated);
	float MorphAlpha = LODCalculated - LodValue;

	// InputPositionLODAdjusted : Position for actual LOD in base LOD units
#if FEATURE_LEVEL >= FEATURE_LEVEL_SM4
	float2 ActualLODCoordsInt = floor(Intermediates.InputPosition.xy * pow(2, -(LodValue - Intermediates.LodValues.x)));
	float InvLODScaleFactor = pow(2, -LodValue);
#else

	// Clamp lod values around holes until we reach HoleMaxLod. Kill morphing when clamped.
	bool bMorph = LodValue < HoleVertexLod || HoleMaxLod <= LodValue;
	MorphAlpha = bMorph ? MorphAlpha : 0;
	bool bClampLodForHole = HoleVertexLod < LodValue && LodValue < HoleMaxLod;
	LodValue = bClampLodForHole ? HoleVertexLod : LodValue;

	// Get range of heights for all LODs
	float MinHeight = DecodePackedHeight(float2(Input.LODHeights0.x, Input.LODHeights0.y));
	float HeightDelta = Input.PackedPosition.w * 255.0 * 256.0 * TERRAIN_ZSCALE;

	// Select the height values from the vertex input corresponding to the current and next LOD
	uint LODHeightIndex = min((uint)LodValue + 2, (uint)7);
	uint LODHeightIndexNextLod = min((uint)LodValue + 3, (uint)7);
	
	float4 LODHeight = LODHeightIndex >> 2 ? Input.LODHeights1 : Input.LODHeights0;
	float InputHeight = LODHeight[LODHeightIndex & 3];
	float4 LODHeightNext = LODHeightIndexNextLod >> 2 ? Input.LODHeights1 : Input.LODHeights0;
	float InputHeightNextLOD = LODHeightNext[LODHeightIndexNextLod & 3];

	float Height = MinHeight + InputHeight * HeightDelta;
	float HeightNextLOD = MinHeight + InputHeightNextLOD * HeightDelta;

	float InvLODScaleFactor = 1.f / (float)(1 << (uint)LodValue);
	// LodValues.x is always 0 on mobile.
	float2 ActualLODCoordsInt = floor(Intermediates.InputPosition.xy * InvLODScaleFactor);
#endif

	// Base to Actual LOD, Base to Next LOD
	float2 CoordTranslate = float2( LandscapeParameters.SubsectionSizeVertsLayerUVPan.x * InvLODScaleFactor - 1, max(LandscapeParameters.SubsectionSizeVertsLayerUVPan.x * 0.5f * InvLODScaleFactor, 2) - 1 ) * LandscapeParameters.SubsectionSizeVertsLayerUVPan.y;
	float2 InputPositionLODAdjusted = ActualLODCoordsInt / CoordTranslate.x;

	// InputPositionNextLOD : Position for next LOD in base LOD units
	float2 NextLODCoordsInt = floor(ActualLODCoordsInt * 0.5);
	float2 InputPositionNextLOD = NextLODCoordsInt / CoordTranslate.y;

	// Get the height and normal XY for current and next LOD out of the textures
#if FEATURE_LEVEL >= FEATURE_LEVEL_SM4
	float2 SampleCoords = InputPositionLODAdjusted * LandscapeParameters.HeightmapUVScaleBias.xy + LandscapeParameters.HeightmapUVScaleBias.zw + 0.5*LandscapeParameters.HeightmapUVScaleBias.xy + Intermediates.InputPosition.zw * LandscapeParameters.SubsectionOffsetParams.xy;
	float4 SampleValue = Texture2DSampleLevel(LandscapeParameters.HeightmapTexture, LandscapeParameters.HeightmapTextureSampler, SampleCoords, LodValue-Intermediates.LodBias.x);
	float Height = DecodePackedHeight(SampleValue.xy);

	float2 SampleCoordsNextLOD = InputPositionNextLOD * LandscapeParameters.HeightmapUVScaleBias.xy + LandscapeParameters.HeightmapUVScaleBias.zw + 0.5*LandscapeParameters.HeightmapUVScaleBias.xy + Intermediates.InputPosition.zw * LandscapeParameters.SubsectionOffsetParams.xy;
	float4 SampleValueNextLOD = Texture2DSampleLevel(LandscapeParameters.HeightmapTexture, LandscapeParameters.HeightmapTextureSampler, SampleCoordsNextLOD, LodValue+1-Intermediates.LodBias.x);
	float HeightNextLOD = DecodePackedHeight(SampleValueNextLOD.xy);
#endif


#if LANDSCAPE_XYOFFSET // FEATURE_LEVEL >= FEATURE_LEVEL_SM4 only
	float2 SampleCoords2 = float2(InputPositionLODAdjusted * LandscapeParameters.WeightmapUVScaleBias.xy + LandscapeParameters.WeightmapUVScaleBias.zw + Intermediates.InputPosition.zw * LandscapeParameters.SubsectionOffsetParams.zz);
	float4 OffsetValue = Texture2DSampleLevel( LandscapeParameters.XYOffsetmapTexture, LandscapeParameters.XYOffsetmapTextureSampler, SampleCoords2, LodValue- Intermediates.LodBias.y );
	float2 SampleCoordsNextLOD2 = float2(InputPositionNextLOD * LandscapeParameters.WeightmapUVScaleBias.xy + LandscapeParameters.WeightmapUVScaleBias.zw + Intermediates.InputPosition.zw * LandscapeParameters.SubsectionOffsetParams.zz);
	float4 OffsetValueNextLOD = Texture2DSampleLevel( LandscapeParameters.XYOffsetmapTexture, LandscapeParameters.XYOffsetmapTextureSampler, SampleCoordsNextLOD2, LodValue+1-Intermediates.LodBias.y );
	float2 XYOffset = float2(((OffsetValue.r * 255.0 * 256.0 + OffsetValue.g * 255.0) - 32768.0) * XYOFFSET_SCALE, ((OffsetValue.b * 255.0 * 256.0 + OffsetValue.a * 255.0) - 32768.0) * XYOFFSET_SCALE );
	float2 XYOffsetNextLOD = float2(((OffsetValueNextLOD.r * 255.0 * 256.0 + OffsetValueNextLOD.g * 255.0) - 32768.0) * XYOFFSET_SCALE, ((OffsetValueNextLOD.b * 255.0 * 256.0 + OffsetValueNextLOD.a * 255.0) - 32768.0) * XYOFFSET_SCALE );

	InputPositionLODAdjusted = InputPositionLODAdjusted + XYOffset;
	InputPositionNextLOD = InputPositionNextLOD + XYOffsetNextLOD;
#endif

	Intermediates.LocalPosition = lerp( float3(InputPositionLODAdjusted, Height), float3(InputPositionNextLOD, HeightNextLOD), MorphAlpha );

#if FEATURE_LEVEL >= FEATURE_LEVEL_SM4
	float2 Normal = float2(SampleValue.b, SampleValue.a);
	float2 NormalNextLOD = float2(SampleValueNextLOD.b, SampleValueNextLOD.a);
	float2 InterpNormal = lerp( Normal, NormalNextLOD, MorphAlpha ) * float2(2.0,2.0) - float2(1.0,1.0);
	Intermediates.WorldNormal = float3( InterpNormal, sqrt(max(1.0-dot(InterpNormal,InterpNormal),0.0)) );
#else
	Intermediates.WorldNormal = float3( 0.0, 0.0, 1.0 );
#endif
	
	return Intermediates;
}


/**
* Get the 3x3 tangent basis vectors for this vertex factory
* this vertex factory will calculate the binormal on-the-fly
*
* @param Input - vertex input stream structure
* @return 3x3 matrix
*/
float3x3 VertexFactoryGetTangentToLocal( FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates )
{
	float3x3 Result = CalcTangentBasisFromWorldNormal(Intermediates.WorldNormal);
	return Result;
}

float4 VertexFactoryGetRasterizedWorldPosition(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, float4 InWorldPosition)
{
	return InWorldPosition;
}

float3 VertexFactoryGetPositionForVertexLighting(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, float3 TranslatedWorldPosition)
{
	return TranslatedWorldPosition;
}

FVertexFactoryInterpolantsVSToPS VertexFactoryGetInterpolantsVSToPS(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, FMaterialVertexParameters VertexParameters)
{
	FVertexFactoryInterpolantsVSToPS Interpolants;

	Interpolants = (FVertexFactoryInterpolantsVSToPS)0;
	FLandscapeTexCoords LandscapeTexCoords = GetLandscapeTexCoords(Input, Intermediates);

#if (ES3_1_PROFILE)
	Interpolants.LayerTexCoord	= LandscapeTexCoords.LayerTexCoord;
	Interpolants.WeightMapTexCoord	= LandscapeTexCoords.WeightMapTexCoord; 
	#if NUM_MATERIAL_TEXCOORDS
		float2 CustomizedUVs[NUM_MATERIAL_TEXCOORDS];
		GetMaterialCustomizedUVs(VertexParameters, CustomizedUVs);

		Interpolants.LayerTexCoord.xy = CustomizedUVs[0];
		#if NUM_MATERIAL_TEXCOORDS > 1
			Interpolants.TransformedTexCoord0.xy	= CustomizedUVs[1];
			#if NUM_MATERIAL_TEXCOORDS > 2
				Interpolants.TransformedTexCoord1.xy	= CustomizedUVs[2];
			#endif // 2
			// ignore 4th CustomizedUV because of weightmap UV
		#endif // 1
	#endif //NUM_MATERIAL_TEXCOORDS
#else
	Interpolants.LayerTexCoord				= LandscapeTexCoords.LayerTexCoord;
	Interpolants.WeightHeightMapTexCoord.xy	= LandscapeTexCoords.WeightMapTexCoord;
	Interpolants.WeightHeightMapTexCoord.zw	= LandscapeTexCoords.HeightMapTexCoord;
	#if NUM_MATERIAL_TEXCOORDS
		float2 CustomizedUVs[NUM_MATERIAL_TEXCOORDS];
		GetMaterialCustomizedUVs(VertexParameters, CustomizedUVs);

		Interpolants.LayerTexCoord.xy = CustomizedUVs[0];
		Interpolants.TransformedTexCoords = 0;

		#if NUM_MATERIAL_TEXCOORDS > 1
			Interpolants.TransformedTexCoords.xy = CustomizedUVs[1];
			#if NUM_MATERIAL_TEXCOORDS > 2
				Interpolants.TransformedTexCoords.zw = CustomizedUVs[2];
			#endif // 2
		#endif // 1

	#endif // NUM_MATERIAL_TEXCOORDS
#endif // (ES3_1_PROFILE)

#if NEEDS_LIGHTMAP_COORDINATE
	#if (ES3_1_PROFILE)
		Interpolants.LightMapCoordinate[0].xy = LandscapeTexCoords.LightMapCoordinate * PrecomputedLightingBuffer.LightMapCoordinateScaleBias.xy + PrecomputedLightingBuffer.LightMapCoordinateScaleBias.zw;
		Interpolants.LightMapCoordinate[0].y *= 0.5;
		Interpolants.LightMapCoordinate[1].xy = Interpolants.LightMapCoordinate[0].xy;
		Interpolants.LightMapCoordinate[1].y += 0.5;
		#if STATICLIGHTING_TEXTUREMASK
			Interpolants.ShadowMapCoordinate.xy	= LandscapeTexCoords.LightMapCoordinate.xy * PrecomputedLightingBuffer.ShadowMapCoordinateScaleBias.xy + PrecomputedLightingBuffer.ShadowMapCoordinateScaleBias.zw;
		#else
			Interpolants.ShadowMapCoordinate.xy = 0;
		#endif
	#else
		Interpolants.LightMapCoordinate.xy		= LandscapeTexCoords.LightMapCoordinate.xy * PrecomputedLightingBuffer.LightMapCoordinateScaleBias.xy + PrecomputedLightingBuffer.LightMapCoordinateScaleBias.zw;
		#if STATICLIGHTING_TEXTUREMASK
			Interpolants.LightMapCoordinate.zw	= LandscapeTexCoords.LightMapCoordinate.xy * PrecomputedLightingBuffer.ShadowMapCoordinateScaleBias.xy + PrecomputedLightingBuffer.ShadowMapCoordinateScaleBias.zw;
		#else
			Interpolants.LightMapCoordinate.zw = 0;
		#endif
	#endif
#endif

#if INSTANCED_STEREO
	Interpolants.EyeIndex = 0;
#endif
	
#if VF_USE_PRIMITIVE_SCENE_DATA
	Interpolants.PrimitiveId = Intermediates.SceneData.PrimitiveId;
#endif

	return Interpolants;
}

FVertexFactoryRayTracingInterpolants VertexFactoryGetRayTracingInterpolants(FVertexFactoryInput Input, FVertexFactoryIntermediates Intermediates, FMaterialVertexParameters VertexParameters)
{
	FVertexFactoryRayTracingInterpolants Interpolants;
	
	Interpolants.InterpolantsVSToPS = VertexFactoryGetInterpolantsVSToPS(Input, Intermediates, VertexParameters);

#if NEEDS_VERTEX_FACTORY_INTERPOLATION
	// Calculate LocalToTangent directly from normal map texture.
	float3x3 TangentToLocal = CalcTangentBasisFromWorldNormal(Intermediates.WorldNormal);
	float3x3 TangentToWorld = mul(TangentToLocal, (float3x3)LandscapeParameters.LocalToWorldNoScaling);
	
	Interpolants.TangentToWorld0 = TangentToWorld[0];
	Interpolants.TangentToWorld2 = float4(TangentToWorld[2], 1);

	float2 xy = Intermediates.InputPosition.xy * Intermediates.LodValues.w;
	Interpolants.CalculatedLOD = CalcLOD(Intermediates.ComponentIndex, xy, Intermediates.InputPosition.zw);
#endif
	
	return Interpolants;
}

FVertexFactoryRayTracingInterpolants VertexFactoryInterpolate(FVertexFactoryRayTracingInterpolants a, float aInterp, FVertexFactoryRayTracingInterpolants b, float bInterp)
{
	FVertexFactoryRayTracingInterpolants O;
	
#if NEEDS_LIGHTMAP_COORDINATE && !(ES3_1_PROFILE)
	INTERPOLATE_MEMBER(InterpolantsVSToPS.LightMapCoordinate);
#endif

	INTERPOLATE_MEMBER(InterpolantsVSToPS.LayerTexCoord);
#if (ES3_1_PROFILE)
	INTERPOLATE_MEMBER(InterpolantsVSToPS.WeightMapTexCoord);
	INTERPOLATE_MEMBER(InterpolantsVSToPS.TransformedTexCoord0);
	INTERPOLATE_MEMBER(InterpolantsVSToPS.TransformedTexCoord1);
#else
	INTERPOLATE_MEMBER(InterpolantsVSToPS.WeightHeightMapTexCoord);
	INTERPOLATE_MEMBER(InterpolantsVSToPS.TransformedTexCoords);
#endif

#if NEEDS_VERTEX_FACTORY_INTERPOLATION
	INTERPOLATE_MEMBER(TangentToWorld0);
	INTERPOLATE_MEMBER(TangentToWorld2);
	INTERPOLATE_MEMBER(CalculatedLOD);
#endif

#if VF_USE_PRIMITIVE_SCENE_DATA
	O.InterpolantsVSToPS.PrimitiveId = a.InterpolantsVSToPS.PrimitiveId;
#endif

	return O;
}

#if NUM_VF_PACKED_INTERPOLANTS > 0
void VertexFactoryPackInterpolants(inout FVertexFactoryInterpolantsVSToPS Interpolants, float4 PackedInterpolants[NUM_VF_PACKED_INTERPOLANTS])
{
	Interpolants.TransformedTexCoord0.zw = PackedInterpolants[0].xy;
	Interpolants.TransformedTexCoord1.zw = PackedInterpolants[0].zw;
}

void VertexFactoryUnpackInterpolants(FVertexFactoryInterpolantsVSToPS Interpolants, out float4 PackedInterpolants[NUM_VF_PACKED_INTERPOLANTS])
{
	PackedInterpolants[0].xy = Interpolants.TransformedTexCoord0.zw;
	PackedInterpolants[0].zw = Interpolants.TransformedTexCoord1.zw;
#if NUM_VF_PACKED_INTERPOLANTS > 1
	UNROLL
	for (int i = 1; i < NUM_VF_PACKED_INTERPOLANTS; ++i)
	{
		PackedInterpolants[i] = 0;
	}
#endif
}
#endif // NUM_VF_PACKED_INTERPOLANTS > 0

float4 VertexFactoryGetTranslatedPrimitiveVolumeBounds(FVertexFactoryInterpolantsVSToPS Interpolants)
{
	return 0;
}

uint VertexFactoryGetPrimitiveId(FVertexFactoryInterpolantsVSToPS Interpolants)
{
#if VF_USE_PRIMITIVE_SCENE_DATA
	return Interpolants.PrimitiveId;
#else // VF_USE_PRIMITIVE_SCENE_DATA
	return 0;
#endif // VF_USE_PRIMITIVE_SCENE_DATA
}

#include "VertexFactoryDefaultInterface.ush"
