// Copyright Epic Games, Inc. All Rights Reserved.

#include "TSRCommon.ush"


//------------------------------------------------------- CONFIG

#define CONFIG_OUTPUT_HALF_RES DIM_OUTPUT_HALF_RES

#define TILE_SIZE 16


//------------------------------------------------------- PARAMETERS

Texture2D<tsr_half> ParallaxRejectionMaskTexture;
Texture2D<tsr_halfC> FilteredInputTexture;
Texture2D<tsr_halfC> FilteredPredictionSceneColorTexture;

RWTexture2D<tsr_half> HistoryRejectionOutput;


//------------------------------------------------------- LDS

groupshared tsr_half SharedRejection[TILE_SIZE * TILE_SIZE];


//------------------------------------------------------- FUNCTIONS

tsr_halfC TransformColorForClampingBox(tsr_halfC Color)
{
	return RGBToYCoCg(Color);
}


//------------------------------------------------------- ENTRY POINT

[numthreads(TILE_SIZE, TILE_SIZE, 1)]
void MainCS(
	uint2 GroupId : SV_GroupID,
	uint GroupThreadIndex : SV_GroupIndex)
{
	float4 Debug = 0.0;

	tsr_short2 InputPixelPos = (
		tsr_short2(InputPixelPosMin) +
		tsr_short2(GroupId) * tsr_short2(TILE_SIZE, TILE_SIZE) +
		Map16x16Tile2x2Lane(GroupThreadIndex));

	tsr_halfC InputMinColor;
	tsr_halfC InputMaxColor;
	tsr_halfC InputCenterColor;
	tsr_half ParallaxRejectionMask;

	{
		InputCenterColor = TransformColorForClampingBox(FilteredInputTexture[InputPixelPos]);
		ParallaxRejectionMask = ParallaxRejectionMaskTexture[InputPixelPos];

		InputMinColor = InputCenterColor;
		InputMaxColor = InputCenterColor;
	}
	
	tsr_half NeigborMaxInterferenceSeedGradient = 0.0;
	
	UNROLL
	for (uint i = 0; i < 9; i++)
	{
		if (i == 4)
		{
			continue;
		}
		
		tsr_short2 SampleInputPixelPos = InputPixelPos + ClampPixelOffset(
			InputPixelPos,
			tsr_short2(kOffsets3x3[i]), tsr_short2(kOffsets3x3[i]),
			InputPixelPosMin, InputPixelPosMax);

		tsr_halfC Color = TransformColorForClampingBox(FilteredInputTexture[SampleInputPixelPos]);

		InputMinColor = min(InputMinColor, Color);
		InputMaxColor = max(InputMaxColor, Color);
	}
	

	tsr_halfC PrevColor = TransformColorForClampingBox(FilteredPredictionSceneColorTexture[InputPixelPos]);

	tsr_half Rejection = 1.0;
	#if 1
	{
		tsr_halfC ClampedPrevColor = clamp(PrevColor, InputMinColor, InputMaxColor);

		Rejection = MeasureRejectionFactor(
			PrevColor, ClampedPrevColor,
			InputCenterColor, InputMinColor, InputMaxColor,
			MeasureBackbufferLDRQuantizationErrorFromHDRLuma(max(PrevColor[0], ClampedPrevColor[0])));
		
		//Debug.rgb = abs(YCoCgToRGB(PrevColor - ClampedPrevColor)) / max3(YCoCgToRGB(InputMaxColor), YCoCgToRGB(InputCenterColor), 0.01);
	}
	#endif

	if (ParallaxRejectionMask < tsr_half(PARALLAX_REJECTION_MASK_THRESHOLD))
	{
		Rejection = tsr_half(1.0);
	}

	#if CONFIG_OUTPUT_HALF_RES
	{
		SharedRejection[GroupThreadIndex] = Rejection;

		GroupMemoryBarrierWithGroupSync();

		UNROLL
		for (uint i = 1; i < 4; i++)
		{
			uint ButterflySwap = i;

			Rejection = min(Rejection, SharedRejection[GroupThreadIndex ^ ButterflySwap]);
		}

		bool bIsValid = all(InputPixelPos <= tsr_short2(InputPixelPosMax)) && all((tsr_ushort2(InputPixelPos - tsr_short2(InputPixelPosMin)) % tsr_ushort(2)) == tsr_ushort(0));

		tsr_ushort2 OutputPixelPos = tsr_ushort2(InputPixelPos - tsr_short2(InputPixelPosMin)) / tsr_ushort(2);
		OutputPixelPos.x = bIsValid ? OutputPixelPos.x : ~tsr_ushort(0);

		HistoryRejectionOutput[OutputPixelPos] = Rejection;
	}
	#else
	{
		bool bIsValid = all(InputPixelPos <= tsr_short2(InputPixelPosMax));

		tsr_ushort2 OutputPixelPos = tsr_ushort2(InputPixelPos - tsr_short2(InputPixelPosMin));
		OutputPixelPos.x = bIsValid ? OutputPixelPos.x : ~tsr_ushort(0);

		HistoryRejectionOutput[OutputPixelPos] = Rejection;
	}
	#endif

	#if DEBUG_OUTPUT
	{
		tsr_short2 OutputPixelPos = InvalidateOutputPixelPos(tsr_ushort2(InputPixelPos), InputPixelPosMax);
		DebugOutput[OutputPixelPos] = Debug;
	}
	#endif
}
