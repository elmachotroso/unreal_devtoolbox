// Copyright Epic Games, Inc. All Rights Reserved.

#pragma once

#define SUPPORT_CONTACT_SHADOWS 0
#define USE_HAIR_LIGHTING 0

// Additional rewiring to make DeferredShadingCommon happy
#define PreIntegratedGF				ReflectionStruct.PreIntegratedGF
#define PreIntegratedGFSampler		GlobalBilinearClampedSampler

#include "../SceneData.ush"
#include "../RayTracing/RayTracingCommon.ush"
#include "../RayTracing/RayTracingDeferredMaterials.ush"
#if !COMPUTESHADER
#include "../RayTracing/RayTracingDeferredShadingCommon.ush"
#include "../RayTracing/RayTracingLightingCommon.ush"
#endif
#include "LumenHardwareRayTracingPayloadCommon.ush"
#include "LumenHardwareRayTracingPipelineCommon.ush"

#if !COMPUTESHADER
#include "LumenHardwareRayTracingPlatformCommon.ush"
#endif

#if COMPUTESHADER
#include "../RayTracing/TraceRayInline.ush"
#endif

float3 CalcPrevWorldPositionFromGPUSceneInstanceIndex(float3 WorldPosition, uint GPUSceneInstanceIndex)
{
	FInstanceSceneData InstanceSceneData = GetInstanceSceneData(GPUSceneInstanceIndex, View.InstanceSceneDataSOAStride);
	float4 LocalPosition = mul(float4(WorldPosition, 1), LWCHackToFloat(InstanceSceneData.WorldToLocal));
	float3 PrevWorldPosition = mul(LocalPosition, LWCHackToFloat(InstanceSceneData.PrevLocalToWorld)).xyz;
	return PrevWorldPosition;
}

float3 CalcLocalPositionFromGPUSceneInstanceIndex(float3 WorldPosition, uint GPUSceneInstanceIndex)
{
	FInstanceSceneData InstanceSceneData = GetInstanceSceneData(GPUSceneInstanceIndex, View.InstanceSceneDataSOAStride);
	float3 LocalPosition = mul(float4(WorldPosition, 1), LWCHackToFloat(InstanceSceneData.WorldToLocal)).xyz;
	return LocalPosition;
}

#ifndef ENABLE_NEAR_FIELD_TRACING
#define ENABLE_NEAR_FIELD_TRACING 1
#endif // ENABLE_NEAR_FIELD_TRACING

#ifndef ENABLE_TRANSLUCENT_SKIP_COUNT
#define ENABLE_TRANSLUCENT_SKIP_COUNT 1
#endif // ENABLE_TRANSLUCENT_SKIP_COUNT

#ifndef LUMEN_HARDWARE_INLINE_RAYTRACING
#define LUMEN_HARDWARE_INLINE_RAYTRACING 0
#endif

// Helper macro to set common entry point for passes that have both inline (CS) and non-inline (RGS) version.
// Given "name" it will create "nameCS" entry point for compute and "nameRGS" for raygen shader.
#if LUMEN_HARDWARE_INLINE_RAYTRACING

#define LUMEN_HARDWARE_RAY_TRACING_ENTRY(name)\
void name##_INTERNAL(uint3 DispatchThreadIndex);\
[numthreads(INLINE_RAY_TRACING_THREAD_GROUP_SIZE_X, INLINE_RAY_TRACING_THREAD_GROUP_SIZE_Y, 1)]\
void name##CS(uint3 DispatchThreadIndex : SV_DispatchThreadID) {\
	name##_INTERNAL(DispatchThreadIndex);}\
void name##_INTERNAL(uint3 DispatchThreadIndex)

#else // LUMEN_HARDWARE_RAYTRACING

#define LUMEN_HARDWARE_RAY_TRACING_ENTRY(name)\
void name##_INTERNAL(uint3 DispatchThreadIndex);\
RAY_TRACING_ENTRY_RAYGEN(name##RGS){\
name##_INTERNAL(DispatchRaysIndex());}\
void name##_INTERNAL(uint3 DispatchThreadIndex)

#endif // LUMEN_HARDWARE_INLINE_RAYTRACING

// Lighting modes
#define LIGHTING_FROM_SURFACE_CACHE 0
#define EVALUATE_MATERIAL 1
#define EVALUATE_MATERIAL_AND_DIRECT_LIGHTING 2
#define EVALUATE_MATERIAL_AND_DIRECT_LIGHTING_AND_SKY_LIGHTING 3

uint GetAtlasId(int Mode)
{
	uint AtlasId = FINAL_LIGHTING_ATLAS_ID;
	if (Mode == EVALUATE_MATERIAL)
	{
		AtlasId = IRRADIANCE_ATLAS_ID;
	}
	else if (Mode == EVALUATE_MATERIAL_AND_DIRECT_LIGHTING)
	{
		AtlasId = INDIRECT_IRRADIANCE_ATLAS_ID;
	}
	return AtlasId;
}

float CalcDitheredDistance(uint2 TraceCoord, float StartDistance, float EndDistance)
{
	const float Dither = InterleavedGradientNoise(TraceCoord, View.StateFrameIndexMod8);
	float DitheredDistance = lerp(StartDistance, EndDistance, Dither);
	return DitheredDistance;
}

struct FHitGroupRootConstants
{
	uint BaseInstanceIndex;
	uint UserData;
};

struct FRayTracedLightingContext
{
	RaytracingAccelerationStructure TLAS;
	StructuredBuffer<FHitGroupRootConstants> HitGroupData;
	FRayCone RayCone;
	uint2 TraceCoord;
	uint LinearCoord;
	uint InstanceMask;
	int TranslucentSkipCountMax;
	uint MaxTraversalIterations; // Max ray tracing traversal iterations on supported platforms
	float FarFieldMaxTraceDistance;
	float3 FarFieldReferencePos;

	uint CullingMode;
	bool bHiResSurface;	// Whether to sample high res surface cache data or low res always resident pages
	bool bAcceptFirstHitAndEndSearch;
	bool bSkipClosestHitShader;
	bool bCalculateHitVelocity;
	bool bUseBookmark;
};

FRayTracedLightingContext CreateRayTracedLightingContext(
	in RaytracingAccelerationStructure TLAS,
	in FRayCone RayCone,
	in uint2 TraceCoord,
	in uint LinearCoord,
	in uint CullingMode,
	in int TranslucentSkipCountMax,
	uint MaxTraversalIterations,
	bool bHiResSurface
)
{
	FRayTracedLightingContext Context;
	Context.TLAS = TLAS;
	Context.RayCone = RayCone;
	Context.TraceCoord = TraceCoord;
	Context.LinearCoord = LinearCoord;
	Context.InstanceMask = RAY_TRACING_MASK_OPAQUE;
	Context.TranslucentSkipCountMax = TranslucentSkipCountMax;
	Context.FarFieldMaxTraceDistance = -1.0;
	Context.FarFieldReferencePos = 0;
	Context.MaxTraversalIterations = MaxTraversalIterations;
	Context.CullingMode = CullingMode;
	Context.bHiResSurface = bHiResSurface;
	Context.bAcceptFirstHitAndEndSearch = false;
	Context.bSkipClosestHitShader = false;
	Context.bCalculateHitVelocity = false;
	Context.bUseBookmark = false;
	return Context;
}

FRayTracedLightingContext CreateRayTracedLightingContext(
	in RaytracingAccelerationStructure TLAS,
	in FRayCone RayCone,
	in uint2 TraceCoord,
	in uint LinearCoord,
	in uint CullingMode,
	in int TranslucentSkipCountMax,
	uint MaxTraversalIterations
)
{
	bool bHiResSurface = false;
	return CreateRayTracedLightingContext(TLAS, RayCone, TraceCoord, LinearCoord, CullingMode, TranslucentSkipCountMax, MaxTraversalIterations, bHiResSurface);
}

struct FRayTracedLightingResult
{
	bool bIsHit;
	bool bIsCompleted; // Was ray tracing completed or stopped due to reaching MaxTraversalIterations
	bool bIsFarField;
	float TraceHitDistance;

	bool bIsRadianceCompleted; // Is radiance computation completed or do we need a fallback shading pass
	float3 Radiance;

	float3 WorldVelocity;
	bool bValidSurfaceCache;
	uint MaterialShaderIndex;
	FLumenRayHitBookmark Bookmark;
};

FRayTracedLightingResult CreateRayTracedLightingResult()
{
	FRayTracedLightingResult Result;
	Result.bIsHit = false;
	Result.bIsCompleted = true;
	Result.bIsFarField = false;
	Result.TraceHitDistance = 0;
	Result.bIsRadianceCompleted = true;
	Result.Radiance = 0;
	Result.WorldVelocity = 0;
	Result.bValidSurfaceCache = false;
	Result.MaterialShaderIndex = RAY_TRACING_DEFERRED_MATERIAL_KEY_INVALID;
	Result.Bookmark.PackedData[0] = 0xFFFFFFFF;
	Result.Bookmark.PackedData[1] = 0xFFFFFFFF;

	return Result;
}

#if !COMPUTESHADER
bool TraceLumenHitLightingRay(
	in RayDesc Ray,
	inout FRayTracedLightingContext Context,
	inout FLumenRayHitBookmark Bookmark,
	inout FPackedMaterialClosestHitPayload Payload
)
{
	uint RayFlags = 0;
	if (Context.bUseBookmark)
	{
		TraceLumenShadingRay(Context.TLAS, RayFlags, Context.InstanceMask, RAY_TRACING_SHADER_SLOT_MATERIAL, RAY_TRACING_NUM_SHADER_SLOTS, 0, Ray, Bookmark, Payload);
	}
	else
	{
		TraceLumenShadingRay(Context.TLAS, RayFlags, Context.InstanceMask, RAY_TRACING_SHADER_SLOT_MATERIAL, RAY_TRACING_NUM_SHADER_SLOTS, 0, Ray, Payload);
	}
	return Payload.IsHit();
}
#endif // !COMPUTESHADER

struct FLumenMinimalRayResult
{
	bool bHit;
	bool bCompleted;
};

FLumenMinimalRayResult TraceLumenMinimalRay(
	in RayDesc Ray,
	inout FRayTracedLightingContext Context,
	inout FLumenMinimalPayload Payload,
	inout FLumenRayHitBookmark Bookmark
)
{
	Payload = (FLumenMinimalPayload)0;

	FLumenMinimalRayResult MinimalRayResult;
	MinimalRayResult.bHit = false;
	MinimalRayResult.bCompleted = true;
	
	// UE_RAY_TRACING_LIGHTWEIGHT_CLOSEST_HIT_SHADER is required to trace TraceLumenMinimalRay for performance reasons
	// Ignored when we are using inline ray tracing
	#if UE_RAY_TRACING_LIGHTWEIGHT_CLOSEST_HIT_SHADER == 0 && !LUMEN_HARDWARE_INLINE_RAYTRACING
	{
		// Return a X hit pattern if define wasn't set
		const bool TileX = Context.TraceCoord.x % 4 < 2;
		const bool TileY = Context.TraceCoord.y % 4 < 2;
		if (TileX ^ TileY)
		{
			Payload.HitT = Ray.TMin;
		}
		else
		{
			Payload.SetMiss();	
		}		

		MinimalRayResult.bHit = Payload.IsHit();
		MinimalRayResult.bCompleted = true;
		return MinimalRayResult;
	}
	#endif

	uint RayFlags = RAY_FLAG_FORCE_OPAQUE;
	RayFlags |= Context.CullingMode;
	RayFlags |= Context.bAcceptFirstHitAndEndSearch ? RAY_FLAG_ACCEPT_FIRST_HIT_AND_END_SEARCH : 0;
	RayFlags |= Context.bSkipClosestHitShader ? RAY_FLAG_SKIP_CLOSEST_HIT_SHADER : 0;

#if LUMEN_HARDWARE_INLINE_RAYTRACING	
	FTraceRayInlineResult TraceResult = TraceRayInline(Context.TLAS, RayFlags, Context.InstanceMask, Ray, Context.MaxTraversalIterations);
	if (TraceResult.IsHit())
	{
		Payload.HitT = TraceResult.HitT;

		const int Primitive = TraceResult.InstanceID;
		Payload.SetPrimitiveIndex(Primitive);

		// Our SBTs always have 2 slots per material. Therefore InstanceContributionToHitGroupIndex has a RAY_TRACING_NUM_SHADER_SLOTS multiplier.
		// But because we only encode 1 material into LumenHardwareRayTracingHitDataBuffer we want to get actual material index.
		uint InstanceContributionIndex = TraceResult.InstanceContributionToHitGroupIndex / RAY_TRACING_NUM_SHADER_SLOTS;
		uint HitGroupIndex = TraceResult.GeometryIndex + InstanceContributionIndex;

		FHitGroupRootConstants HitData = Context.HitGroupData[HitGroupIndex];

		const int PrimitiveInstanceIndex = TraceResult.InstanceIndex - HitData.BaseInstanceIndex;
		Payload.SetInstanceIndex(PrimitiveInstanceIndex);

		uint MaterialShaderIndex = HitData.UserData & 0x3FFFFFFF;
		Payload.SetMaterialShaderIndex(MaterialShaderIndex);

		uint bIsTranslucent = (HitData.UserData >> 31) & 0x1;
		Payload.SetIsTranslucent(bIsTranslucent);

		uint bIsTwoSided = (HitData.UserData >> 30) & 0x1;
		Payload.SetIsTwoSided(bIsTwoSided);

		float3 WorldNormal = TraceResult.WorldGeometryNormal;
		Payload.SetWorldNormal(WorldNormal);

		Bookmark.PackedData = TraceResult.Bookmark;
	}
	else
	{
		Payload.SetMiss();
	}
	MinimalRayResult.bCompleted = TraceResult.bIsCompleted;
#else
	{
		TraceLumenRay(Context.TLAS, RayFlags, Context.InstanceMask, RAY_TRACING_SHADER_SLOT_MATERIAL, RAY_TRACING_NUM_SHADER_SLOTS, 0, Ray, Payload, Bookmark);
	}
#endif // LUMEN_HARDWARE_INLINE_RAYTRACING

	MinimalRayResult.bHit = Payload.IsHit();
	return MinimalRayResult;
}

FLumenMinimalRayResult TraceLumenMinimalRay(
	in RayDesc Ray,
	inout FRayTracedLightingContext Context,
	inout FLumenMinimalPayload Payload
)
{
	FLumenRayHitBookmark Bookmark;
	return TraceLumenMinimalRay(Ray, Context, Payload, Bookmark);
}

FLumenMinimalRayResult TraceLumenMinimalRayWithRetries(
	RayDesc Ray,
	inout FRayTracedLightingContext Context,
	inout FLumenMinimalPayload Payload,
	inout FLumenRayHitBookmark Bookmark)
{
	FLumenMinimalRayResult MinimalRayResult = TraceLumenMinimalRay(Ray, Context, Payload, Bookmark);

#if ENABLE_TRANSLUCENT_SKIP_COUNT
	int TranslucentSkipCount = 0;
	while (MinimalRayResult.bHit && Payload.IsTranslucent() && TranslucentSkipCount < Context.TranslucentSkipCountMax)
	{
		float Epsilon = 0.01;
		Ray.TMin = Payload.HitT + Epsilon;

		MinimalRayResult = TraceLumenMinimalRay(Ray, Context, Payload, Bookmark);

		TranslucentSkipCount++;
	}
#endif // ENABLE_TRANSLUCENT_SKIP_COUNT

	return MinimalRayResult;
}

float3 CalculateSurfaceCacheLighting(
	in RayDesc Ray,
	in FRayTracedLightingContext Context,
	float3 RayHitWorldPosition,
	float3 RayHitWorldNormal,
	float HitDistance,
	uint SceneInstanceIndex,
	float3 DiffuseColor,
	int LightingMode,
	inout bool bValidSurfaceCache
)
{
	float3 Radiance = 0;

	float InterpolateRadius = tan(Context.RayCone.SpreadAngle) * HitDistance;
	// Mode and AtlasId enums are coupled
	uint AtlasId = GetAtlasId(LightingMode);
	// FarField proxies are less accurate
	float SurfaceCacheBias = (Context.InstanceMask == RAY_TRACING_MASK_FAR_FIELD) ? 1000.0f : 20.0f;

	const uint MeshCardsIndex = GetMeshCardsIndexFromSceneInstanceIndex(SceneInstanceIndex);

	FSurfaceCacheSample SurfaceCacheSample = SampleLumenMeshCards(
		Context.TraceCoord,
		MeshCardsIndex,
		RayHitWorldPosition,
		RayHitWorldNormal,
		InterpolateRadius,
		SurfaceCacheBias,
		Context.bHiResSurface,
		AtlasId
	);
	bValidSurfaceCache = SurfaceCacheSample.bValid;

	// Apply shading when evaluating the material
	if (LightingMode != LIGHTING_FROM_SURFACE_CACHE)
	{
		SurfaceCacheSample.Lighting *= Diffuse_Lambert(DiffuseColor);
	}
	Radiance += SurfaceCacheSample.Lighting;

	return Radiance;
}

float3 CalculateRayTracedLightingFromSurfaceCache(
	in RayDesc Ray,
	in FRayTracedLightingContext Context,
	FLumenMinimalPayload Payload,
	float HitDistance,
	uint SceneInstanceIndex,
	inout uint bValidSurfaceCache
)
{
	float3 RayHitWorldNormal = Payload.GetWorldNormal();
	float3 RayHitWorldPosition = Ray.Origin + Ray.Direction * HitDistance;
	float3 DiffuseColor = 0;
	float3 SurfaceCacheRadiance = CalculateSurfaceCacheLighting(Ray, Context, RayHitWorldPosition, RayHitWorldNormal, HitDistance, SceneInstanceIndex, DiffuseColor, LIGHTING_FROM_SURFACE_CACHE, bValidSurfaceCache);
	
	float3 Radiance = 0;

	// The Surface Cache lighting was computed using the front face, don't leak to the backface
	if (dot(Ray.Direction, RayHitWorldNormal) <= 0 || Payload.IsTwoSided())
	{
		Radiance = SurfaceCacheRadiance;
	}

	return Radiance;
}

#if !COMPUTESHADER
bool TraceDeferredMaterialRay(
	in RayDesc Ray,
	inout FRayTracedLightingContext Context,
	inout FDeferredMaterialPayload DeferredMaterialPayload
)
{
	DeferredMaterialPayload = (FDeferredMaterialPayload)0;
	DeferredMaterialPayload.SortKey = RAY_TRACING_DEFERRED_MATERIAL_KEY_RAY_MISS;
	DeferredMaterialPayload.PixelCoordinates = (Context.TraceCoord.y << 16) | Context.TraceCoord.x;

	uint RayFlags = RAY_FLAG_FORCE_OPAQUE;
	RayFlags |= Context.CullingMode;

	FRayIntersectionBookmark Bookmark = (FRayIntersectionBookmark)0;
	TraceDeferredMaterialGatherRay(Context.TLAS, RayFlags, Context.InstanceMask, Ray, Bookmark, DeferredMaterialPayload);

	return DeferredMaterialPayload.IsHit();
}

float3 CalculateDirectLighting(
	in RayDesc Ray,
	in FRayTracedLightingContext Context,
	inout FPackedMaterialClosestHitPayload Payload,
	float3 RayHitWorldPosition,
	float3 RayHitWorldNormal
)
{
	RandomSequence RandSequence;
	uint SampleOffset = 0;
	RandomSequence_Initialize(RandSequence, Context.LinearCoord, View.StateFrameIndex + SampleOffset * 16);
	float MaxNormalBias = 0.05;
	uint ReflectedShadowsType = 2;
	bool bShouldDoDirectLighting = true;
	bool bShouldDoEmissiveAndIndirectLighting = false;
	bool bTopLayerRayTraceSkyLightContribution = false;
	bool bDecoupleSampleGeneration = false;

	float3 DirectLighting = 0.0f;
	AccumulateResults(
		Payload,
		RayHitWorldPosition,
		Ray.Direction,
		Context.TLAS,
		RandSequence,
		Context.TraceCoord,
		MaxNormalBias,
		ReflectedShadowsType,
		bShouldDoDirectLighting,
		bShouldDoEmissiveAndIndirectLighting,
		bTopLayerRayTraceSkyLightContribution,
		bDecoupleSampleGeneration,
		Context.RayCone,
		DirectLighting);
	return DirectLighting;
}

float3 CalculateRayTracedLighting(
	in RayDesc Ray,
	in FRayTracedLightingContext Context,
	inout FPackedMaterialClosestHitPayload Payload,
	float HitDistance,
	uint SceneInstanceIndex,
	int LightingMode
)
{
	// Apply emissive material
	float3 Radiance = Payload.GetRadiance() * Payload.GetOpacity();

	float3 RayHitWorldNormal = Payload.GetWorldNormal();

	float3 RayHitWorldPosition = Ray.Origin + Ray.Direction * HitDistance;
	if (LightingMode >= EVALUATE_MATERIAL_AND_DIRECT_LIGHTING)
	{
		Radiance += CalculateDirectLighting(Ray, Context, Payload, RayHitWorldPosition, RayHitWorldNormal);
	}

	float3 DiffuseColor = Payload.GetDiffuseColor();

	if (LightingMode == EVALUATE_MATERIAL_AND_DIRECT_LIGHTING_AND_SKY_LIGHTING)
	{
		Radiance += DiffuseColor * Payload.GetIndirectIrradiance();
	}
	else
	{
		bool bValidSurfaceCache = false;
		float3 SurfaceCacheRadiance = CalculateSurfaceCacheLighting(Ray, Context, RayHitWorldPosition, RayHitWorldNormal, HitDistance, SceneInstanceIndex, DiffuseColor, LightingMode, bValidSurfaceCache);

		// The Surface Cache lighting was computed using the front face, don't leak to the backface
		if (dot(Ray.Direction, RayHitWorldNormal) <= 0 || (Payload.GetFlags() | RAY_TRACING_PAYLOAD_OUTPUT_FLAG_TWO_SIDED))
		{
			Radiance += SurfaceCacheRadiance;
		}
	}
	return Radiance;
}

FRayTracedLightingResult TraceAndCalculateRayTracedLighting(
	in RayDesc Ray,
	in FRayTracedLightingContext Context,
	int LightingMode,
	inout FLumenRayHitBookmark Bookmark
)
{
	FRayTracedLightingResult Result = CreateRayTracedLightingResult();
	Result.TraceHitDistance = Ray.TMax;

	uint RayFlags = 0;

	FPackedMaterialClosestHitPayload Payload = (FPackedMaterialClosestHitPayload)0;
	Payload.SetLumenPayload();
	Payload.SetIgnoreTranslucentMaterials();
	if (LightingMode == LIGHTING_FROM_SURFACE_CACHE)
	{
		Payload.SetMinimalPayloadMode();
	}
	else if (LightingMode == EVALUATE_MATERIAL_AND_DIRECT_LIGHTING_AND_SKY_LIGHTING)
	{
		Payload.SetEnableSkyLightContribution();
	}

	// Permute ray origin if tracing against far-field
	float3 RayOrigin = Ray.Origin;
	Ray.Origin += Context.InstanceMask == RAY_TRACING_MASK_FAR_FIELD ? Context.FarFieldReferencePos : 0.0;
	TraceLumenHitLightingRay(Ray, Context, Bookmark, Payload);

	Ray.Origin = RayOrigin;
	Result.bIsHit = Payload.IsHit();
	if (Result.bIsHit)
	{
		uint SceneInstanceIndex = Payload.GetSceneInstanceIndex();
		Result.TraceHitDistance = Payload.HitT;
		Result.Radiance = CalculateRayTracedLighting(Ray, Context, Payload, Result.TraceHitDistance, SceneInstanceIndex, LightingMode);
	}

	return Result;
}

FRayTracedLightingResult TraceAndCalculateRayTracedLighting(
	in RayDesc Ray,
	inout FRayTracedLightingContext Context,
	int LightingMode
)
{
	Context.bUseBookmark = false;
	FLumenRayHitBookmark Bookmark;
	return TraceAndCalculateRayTracedLighting(Ray, Context, LightingMode, Bookmark);
}

#endif // !COMPUTESHADER

FRayTracedLightingResult TraceAndCalculateRayTracedLightingFromSurfaceCache(
	in RayDesc Ray,
	in FRayTracedLightingContext Context
)
{
	FRayTracedLightingResult Result = CreateRayTracedLightingResult();
	Result.TraceHitDistance = Ray.TMax;

	FLumenMinimalPayload Payload = (FLumenMinimalPayload)0;

#if ENABLE_NEAR_FIELD_TRACING
	{
		FLumenMinimalRayResult MinimalRayResult = TraceLumenMinimalRayWithRetries(Ray, Context, Payload, Result.Bookmark);
		Result.bIsHit = MinimalRayResult.bHit;
		Result.bIsCompleted = MinimalRayResult.bCompleted;
	}

	if (Result.bIsHit)
	{
		Result.TraceHitDistance = Payload.HitT;
		Result.MaterialShaderIndex = Payload.GetMaterialShaderIndex();

		if (Context.bCalculateHitVelocity)
		{
			uint GPUSceneInstanceIndex = GetGPUSceneInstanceIndex(Payload.GetPrimitiveIndex(), Payload.GetInstanceIndex());
			float3 WorldPosition = Ray.Origin + Ray.Direction * Payload.HitT;
			Result.WorldVelocity = WorldPosition - CalcPrevWorldPositionFromGPUSceneInstanceIndex(WorldPosition, GPUSceneInstanceIndex);
		}

		uint SceneInstanceIndex = GetGPUSceneInstanceIndex(Payload.GetPrimitiveIndex(), Payload.GetInstanceIndex());
		Result.Radiance = CalculateRayTracedLightingFromSurfaceCache(Ray, Context, Payload, Result.TraceHitDistance, SceneInstanceIndex, Result.bValidSurfaceCache);
	}
#endif

#if ENABLE_FAR_FIELD_TRACING
	if (!Result.bIsHit && Result.bIsCompleted)
	{
		float3 NonRebasedRayOrigin = Ray.Origin;

		Ray.Origin += Context.FarFieldReferencePos;
		Ray.TMin = Ray.TMax;
		Ray.TMax = Context.FarFieldMaxTraceDistance;
		Context.InstanceMask = RAY_TRACING_MASK_FAR_FIELD;

		FLumenMinimalRayResult MinimalRayResult = TraceLumenMinimalRay(Ray, Context, Payload, Result.Bookmark);
		Result.bIsHit = MinimalRayResult.bHit;
		Result.bIsCompleted = MinimalRayResult.bCompleted;

		Ray.Origin = NonRebasedRayOrigin;

		if (Result.bIsHit)
		{
			Result.TraceHitDistance = Payload.HitT;
			Result.MaterialShaderIndex = Payload.GetMaterialShaderIndex();
			Result.bIsFarField = true;

			uint SceneInstanceIndex = GetGPUSceneInstanceIndex(Payload.GetPrimitiveIndex(), Payload.GetInstanceIndex());
			Result.Radiance = CalculateRayTracedLightingFromSurfaceCache(Ray, Context, Payload, Result.TraceHitDistance, SceneInstanceIndex, Result.bValidSurfaceCache);
		}
	}
#endif // ENABLE_FAR_FIELD_TRACING

	Result.bIsRadianceCompleted = Result.bValidSurfaceCache;

	// Force a black hit when reached max iterations without hitting anything
	if (!Result.bIsHit && !Result.bIsCompleted)
	{
		Result.bIsHit = true;
		Result.TraceHitDistance = Ray.TMax;
		Result.bIsRadianceCompleted = true;
		Result.Radiance = float3(0.0f, 0.0f, 0.0f);
	}

	return Result;
}