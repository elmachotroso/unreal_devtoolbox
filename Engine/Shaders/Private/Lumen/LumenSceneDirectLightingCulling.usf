// Copyright Epic Games, Inc. All Rights Reserved.

/*=============================================================================
	LumenSceneDirectLightingCulling.usf
=============================================================================*/

#include "../Common.ush"
#define SUPPORT_CONTACT_SHADOWS 0
#include "../DeferredLightingCommon.ush"

#include "LumenCardCommon.ush"
#include "LumenCardTile.ush"
#include "LumenSceneDirectLighting.ush"

#ifndef THREADGROUP_SIZE
#define THREADGROUP_SIZE 1
#endif

StructuredBuffer<uint> CardPageIndexAllocator;
StructuredBuffer<uint> CardPageIndexData;
RWStructuredBuffer<uint> RWCardTileAllocator;
RWStructuredBuffer<uint> RWCardTiles;

/**
 * Splice card pages into N card tiles
 */
[numthreads(THREADGROUP_SIZE, THREADGROUP_SIZE, 1)]
void SpliceCardPagesIntoTilesCS(
	uint3 GroupId : SV_GroupID,
	uint3 DispatchThreadId : SV_DispatchThreadID,
	uint3 GroupThreadId : SV_GroupThreadID)
{
	// One thread per tile
	uint LinearLightTileOffset = (GroupId.x % 4);
	uint IndexInIndexBuffer = GroupId.x / 4;

	uint2 TileCoord;
	TileCoord.x = (LinearLightTileOffset % 2) * 8 + GroupThreadId.x;
	TileCoord.y = (LinearLightTileOffset / 2) * 8 + GroupThreadId.y;

	if (IndexInIndexBuffer < CardPageIndexAllocator[0])
	{
		uint CardPageIndex = CardPageIndexData[IndexInIndexBuffer];
		FLumenCardPageData CardPage = GetLumenCardPageData(CardPageIndex);
		if (CardPage.CardIndex >= 0)
		{
			FLumenCardData Card = GetLumenCardData(CardPage.CardIndex);

			const uint2 SizeInTiles = CardPage.SizeInTexels / CARD_TILE_SIZE;

			if (all(TileCoord < SizeInTiles))
			{
				FCardTileData CardTile;
				CardTile.CardPageIndex = CardPageIndex;
				CardTile.TileCoord = TileCoord;

				uint CardTileIndex = 0;
				InterlockedAdd(RWCardTileAllocator[0], 1, CardTileIndex);
				RWCardTiles[CardTileIndex] = PackCardTileData(CardTile);
			}
		}
	}
}

StructuredBuffer<uint> CardTileAllocator;
StructuredBuffer<uint> CardTiles;
RWBuffer<uint> RWDispatchCardTilesIndirectArgs;

[numthreads(THREADGROUP_SIZE, 1, 1)]
void InitializeCardTileIndirectArgsCS(uint3 DispatchThreadId : SV_DispatchThreadID)
{
	if (DispatchThreadId.x == 0)
	{
		uint NumCardTiles = CardTileAllocator[0];

		// One thread per card tile
		RWDispatchCardTilesIndirectArgs[0] = (NumCardTiles + 63) / 64;
		RWDispatchCardTilesIndirectArgs[1] = 1;
		RWDispatchCardTilesIndirectArgs[2] = 1;
	}
}

bool SphereIntersectCone(float4 SphereCenterAndRadius, float3 ConeVertex, float3 ConeAxis, float ConeAngleCos, float ConeAngleSin)
{
	float3 U = ConeVertex - (SphereCenterAndRadius.w / ConeAngleSin) * ConeAxis;
	float3 D = SphereCenterAndRadius.xyz - U;
	float DSizeSq = dot(D, D);
	float E = dot(ConeAxis, D);

	if (E > 0 && E * E >= DSizeSq * ConeAngleCos * ConeAngleCos)
	{
		D = SphereCenterAndRadius.xyz - ConeVertex;
		DSizeSq = dot(D, D);
		E = -dot(ConeAxis, D);

		if (E > 0 && E * E >= DSizeSq * ConeAngleSin * ConeAngleSin)
		{
			return DSizeSq <= SphereCenterAndRadius.w * SphereCenterAndRadius.w;
		}
		else
		{
			return true;
		}
	}

	return false;
}

bool DoesLightAffectCardPageUVRange(FLumenLight LumenLight, FLumenCardPageData CardPage, FLumenCardData Card, float2 UVMin, float2 UVMax)
{
	float3 CardPageLocalCenter;
	float3 CardPageLocalExtent;
	GetCardLocalBBox(CardPage, Card, UVMin, UVMax, CardPageLocalCenter, CardPageLocalExtent);

	float3 CardPageWorldCenter = mul(Card.WorldToLocalRotation, CardPageLocalCenter) + Card.Origin;
	float3 CardPageWorldExtent = mul(abs(Card.WorldToLocalRotation), CardPageLocalExtent);
	float CardPageWorldBoundingSphere = length(CardPageLocalExtent);

	float4 InfluenceSphere = LumenLight.InfluenceSphere;
	float3 LightInfluenceSphereLocalCenter = mul(InfluenceSphere.xyz - Card.Origin, Card.WorldToLocalRotation);
	const float BoxDistanceSq = ComputeSquaredDistanceFromBoxToPoint(CardPageLocalCenter, CardPageLocalExtent, LightInfluenceSphereLocalCenter);
	const bool bCardAffectedByInfluenceSphere = BoxDistanceSq < InfluenceSphere.w * InfluenceSphere.w;

	const uint LightType = LumenLight.Type;
	const float3 LightPosition = LumenLight.ProxyPosition;
	const float3 LightDirection = LumenLight.ProxyDirection;
	const float LightRadius = LumenLight.ProxyRadius;

	// Fast out
	if (LightType != LIGHT_TYPE_DIRECTIONAL && !bCardAffectedByInfluenceSphere)
	{
		return false;
	}

	if (LightType == LIGHT_TYPE_DIRECTIONAL)
	{
		return true;
	}
	else if (LightType == LIGHT_TYPE_POINT)
	{
		// Point light
		return bCardAffectedByInfluenceSphere;
	}
	else if (LightType == LIGHT_TYPE_SPOT)
	{
		float CosConeAngle = LumenLight.CosConeAngle;
		float SinConeAngle = LumenLight.SinConeAngle;

		float ConeAxisDistance = dot(CardPageWorldCenter - LightPosition, LightDirection);
		float2 ConeAxisDistanceMinMax = float2(ConeAxisDistance + CardPageWorldBoundingSphere, ConeAxisDistance - CardPageWorldBoundingSphere);

		// Spot light
		return bCardAffectedByInfluenceSphere
			&& SphereIntersectCone(float4(CardPageWorldCenter, CardPageWorldBoundingSphere), LightPosition, LightDirection, CosConeAngle, SinConeAngle)
			&& ConeAxisDistanceMinMax.x > 0 && ConeAxisDistanceMinMax.y < LightRadius;
	}
	else if (LightType == LIGHT_TYPE_RECT)
	{
		// Rect light
		float4 BackPlane = float4(LightDirection, dot(LightPosition, LightDirection));
		float DistanceFromBoxCenterToPlane = dot(BackPlane.xyz, CardPageWorldCenter) - BackPlane.w;
		float MaxExtent = dot(CardPageWorldExtent, abs(BackPlane.xyz));
		bool bInFrontOfPlane = DistanceFromBoxCenterToPlane + MaxExtent > 0.0f;
		return bCardAffectedByInfluenceSphere && bInFrontOfPlane;
	}

	// Error: Unknown light type
	return false;
}

bool DoesLightAffectCardPage(FLumenLight LumenLight, FLumenCardPageData CardPage, FLumenCardData Card)
{
	float2 UVMin = float2(0.0f, 0.0f);
	float2 UVMax = float2(1.0f, 1.0f);
	return DoesLightAffectCardPageUVRange(LumenLight, CardPage, Card, UVMin, UVMax);
}

RWStructuredBuffer<uint> RWLightTileAllocator;
RWStructuredBuffer<uint2> RWLightTiles;
RWStructuredBuffer<uint> RWLightTileAllocatorPerLight;
RWStructuredBuffer<uint> RWLightAllocatorPerTile;

uint MaxLightsPerTile;
uint NumLights;

/**
 * Pick N most important lights per tile in page selected to update to update this frame, and output a list of light tiles
 */
[numthreads(THREADGROUP_SIZE, 1, 1)]
void BuildLightTilesCS(
	uint3 GroupId : SV_GroupID,
	uint3 DispatchThreadId : SV_DispatchThreadID,
	uint3 GroupThreadId : SV_GroupThreadID)
{
	// One thread per tile
	uint CardTileIndex = DispatchThreadId.x;

	if (CardTileIndex < CardTileAllocator[0])
	{
		FCardTileData CardTile = UnpackCardTileData(CardTiles[CardTileIndex]);
		FLumenCardPageData CardPage = GetLumenCardPageData(CardTile.CardPageIndex);

		if (CardPage.CardIndex >= 0)
		{
			FLumenCardData Card = GetLumenCardData(CardPage.CardIndex);

			const uint2 SizeInTiles = CardPage.SizeInTexels / CARD_TILE_SIZE;
			float2 UVMin = float2(CardTile.TileCoord) / SizeInTiles;
			float2 UVMax = float2(CardTile.TileCoord + 1) / SizeInTiles;

			float SwapY = UVMin.y;
			UVMin.y = 1.0f - UVMax.y;
			UVMax.y = 1.0f - SwapY;

			// Loop over lights to select N most important lights
			uint NumCulledLightsPerTile = 0;
			for (uint LightIndex = 0; LightIndex < NumLights; ++LightIndex)
			{
				FLumenLight LumenLight = LoadLumenLight(LightIndex);

				bool bLightAffectsCard = DoesLightAffectCardPageUVRange(LumenLight, CardPage, Card, UVMin, UVMax);
				if (bLightAffectsCard)
				{
					// Write light tile
					FLightTile LightTile;
					LightTile.LightIndex = LightIndex;
					LightTile.CardPageIndex = CardTile.CardPageIndex;
					LightTile.TileCoord = CardTile.TileCoord;

					// Add to global light tile array
					uint CardTileIndex = 0;
					InterlockedAdd(RWLightTileAllocator[0], 1, CardTileIndex);
					RWLightTiles[CardTileIndex] = PackLightTile(LightTile);

					InterlockedAdd(RWLightTileAllocatorPerLight[LightIndex], 1);

					// Stop when we reached the limit of cards per current tile
					++NumCulledLightsPerTile;
					if (NumCulledLightsPerTile >= MaxLightsPerTile)
					{
						break;
					}
				}
			}
		}
	}
}

StructuredBuffer<uint> LightTileAllocatorPerLight;
RWStructuredBuffer<uint> RWLightTileOffsetsPerLight;

/**
 * Prefix sum for card tile array compaction
 */
[numthreads(THREADGROUP_SIZE, 1, 1)]
void ComputeLightTileOffsetsPerLightCS(uint3 DispatchThreadId : SV_DispatchThreadID)
{
	if (DispatchThreadId.x == 0)
	{
		uint LightOffset = 0;
		for (uint LightIndex = 0; LightIndex < NumLights; ++LightIndex)
		{
			RWLightTileOffsetsPerLight[LightIndex] = LightOffset;
			LightOffset += LightTileAllocatorPerLight[LightIndex];
		}
	}
}

RWStructuredBuffer<uint2> RWCompactedLightTiles;
RWStructuredBuffer<uint> RWCompactedLightTileAllocatorPerLight;
StructuredBuffer<uint> LightTileAllocator;
StructuredBuffer<uint2> LightTiles;
StructuredBuffer<uint> LightTileOffsetsPerLight;

/**
 * Compact card tile array
 */
[numthreads(THREADGROUP_SIZE, 1, 1)]
void CompactLightTilesCS(uint3 DispatchThreadId : SV_DispatchThreadID)
{
	const uint LightTileIndex = DispatchThreadId.x;

	if (LightTileIndex < LightTileAllocator[0])
	{
		const FLightTile LightTile = UnpackLightTile(LightTiles[LightTileIndex]);

		uint CompactedLightTileIndex = 0;
		InterlockedAdd(RWCompactedLightTileAllocatorPerLight[LightTile.LightIndex], 1, CompactedLightTileIndex);
		CompactedLightTileIndex += LightTileOffsetsPerLight[LightTile.LightIndex];

		RWCompactedLightTiles[CompactedLightTileIndex] = PackLightTile(LightTile);
	}
}

RWBuffer<uint> RWDispatchLightTilesIndirectArgs;
RWBuffer<uint> RWDrawTilesPerLightIndirectArgs;
RWBuffer<uint> RWDispatchTilesPerLightIndirectArgs;

uint VertexCountPerInstanceIndirect;

[numthreads(THREADGROUP_SIZE, 1, 1)]
void InitializeLightTileIndirectArgsCS(uint3 DispatchThreadId : SV_DispatchThreadID)
{
	uint LightIndex = DispatchThreadId.x;

	// Global card tile array
	if (LightIndex == 0)
	{
		uint NumLightTiles = LightTileAllocator[0];

		// One group per tile
		RWDispatchLightTilesIndirectArgs[0] = NumLightTiles;
		RWDispatchLightTilesIndirectArgs[1] = 1;
		RWDispatchLightTilesIndirectArgs[2] = 1;

		// One thread per tile
		RWDispatchLightTilesIndirectArgs[3 + 0] = (NumLightTiles + 63) / 64;
		RWDispatchLightTilesIndirectArgs[3 + 1] = 1;
		RWDispatchLightTilesIndirectArgs[3 + 2] = 1;
	}

	// Per light card tile array
	if (LightIndex < NumLights)
	{
		uint NumLightTilesPerLight = LightTileAllocatorPerLight[LightIndex];

		// FRHIDispatchIndirectParameters
		RWDispatchTilesPerLightIndirectArgs[3 * LightIndex + 0] = NumLightTilesPerLight;
		RWDispatchTilesPerLightIndirectArgs[3 * LightIndex + 1] = 1;
		RWDispatchTilesPerLightIndirectArgs[3 * LightIndex + 2] = 1;

		// FRHIDrawIndirectParameters
		RWDrawTilesPerLightIndirectArgs[4 * LightIndex + 0] = VertexCountPerInstanceIndirect;
		RWDrawTilesPerLightIndirectArgs[4 * LightIndex + 1] = NumLightTilesPerLight;
		RWDrawTilesPerLightIndirectArgs[4 * LightIndex + 2] = 0;
		RWDrawTilesPerLightIndirectArgs[4 * LightIndex + 3] = 0;
	}
}

uint LightIndex;

void RasterizeToLightTilesVS(
	uint VertexId : SV_VertexID,
	uint InstanceId : SV_InstanceID,
	out FCardVSToPS CardInterpolants,
	out float4 OutPosition : SV_POSITION
)
{
	float2 TexCoord = float2(0.0f, 0.0f);
	TexCoord.x += VertexId == 1 || VertexId == 2 || VertexId == 4 ? 1.0f : 0.0f;
	TexCoord.y += VertexId == 2 || VertexId == 4 || VertexId == 5 ? 1.0f : 0.0f;

	uint LightTileIndex = InstanceId.x + LightTileOffsetsPerLight[LightIndex];
	uint NumLightTiles = LightTileAllocator[0];

	CardInterpolants = (FCardVSToPS)0;
	OutPosition = 0;

	if (LightTileIndex < NumLightTiles)
	{
		FLightTile LightTile = UnpackLightTile(LightTiles[LightTileIndex]);
		FLumenCardPageData CardPage = GetLumenCardPageData(LightTile.CardPageIndex);

		float2 AtlasUV = CardPage.PhysicalAtlasUVRect.xy + CardPage.PhysicalAtlasUVTexelScale * CARD_TILE_SIZE * (LightTile.TileCoord + TexCoord);
		float2 CardUV = CardPage.CardUVRect.xy + CardPage.CardUVTexelScale * CARD_TILE_SIZE * (LightTile.TileCoord + TexCoord);

		float2 ScreenPosition = float2(2.0f, -2.0f) * AtlasUV + float2(-1.0f, 1.0f);
		OutPosition = float4(ScreenPosition, 0, 1);

		CardInterpolants.AtlasUV = AtlasUV;
		CardInterpolants.IndirectLightingAtlasUV = float2(0.0f, 0.0f);
		CardInterpolants.CardUV = CardUV;
		CardInterpolants.CardTileIndex = LightTileIndex;
		CardInterpolants.CardPageIndex = LightTile.CardPageIndex;
	}
}
