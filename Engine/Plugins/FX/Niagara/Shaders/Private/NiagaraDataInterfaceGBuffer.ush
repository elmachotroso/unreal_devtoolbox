// Copyright Epic Games, Inc. All Rights Reserved.

#pragma once

#include "/Engine/Private/DeferredShadingCommon.ush"

#define NDIGBUFFER_HAS_GBUFFER				(FEATURE_LEVEL >= FEATURE_LEVEL_SM5)
#define NDIGBUFFER_USE_GBUFFER_VELOCITY		(false && NDIGBUFFER_HAS_GBUFFER)
#define NDIGBUFFER_IS_MOBILE				(FEATURE_LEVEL <= FEATURE_LEVEL_ES3_1)

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

Texture2D NDIGBuffer_VelocityTexture;
SamplerState NDIGBuffer_VelocityTextureSampler;

float2 DIGBuffer_ApplyViewportOffset(float2 UV, bool bApplyViewportOffset)
{
	if (bApplyViewportOffset)
	{
		UV = (UV * float2(2.0f, -2.0f) + float2(-1.0f, 1.0f)) * View.ScreenPositionScaleBias.xy + View.ScreenPositionScaleBias.wz;
	}
	return UV;
}

void DIGBuffer_DecodeDiffuseColor(in float2 ScreenUV, in bool bApplyViewportOffset, out bool IsValid, out float3 OutValue)
{
#if NDIGBUFFER_HAS_GBUFFER
	ScreenUV = DIGBuffer_ApplyViewportOffset(ScreenUV, bApplyViewportOffset);
	FGBufferData GBufferData = GetGBufferData(ScreenUV);
	IsValid = true;
	OutValue = GBufferData.DiffuseColor;
#else
	IsValid = false;
	OutValue = 0;
#endif
}

void DIGBuffer_DecodeWorldNormal(in float2 ScreenUV, in bool bApplyViewportOffset, out bool IsValid, out float3 OutValue)
{
#if NDIGBUFFER_HAS_GBUFFER
	ScreenUV = DIGBuffer_ApplyViewportOffset(ScreenUV, bApplyViewportOffset);
	FGBufferData GBufferData = GetGBufferData(ScreenUV);
	IsValid = true;
	OutValue = GBufferData.WorldNormal;
#else
	IsValid = false;
	OutValue = 0;
#endif
}

void DIGBuffer_DecodeScreenVelocity(in float2 ScreenUV, in bool bApplyViewportOffset, out bool IsValid, out float3 OutValue)
{
	IsValid = false;
	OutValue = 0;
#if NDIGBUFFER_HAS_GBUFFER
	ScreenUV = DIGBuffer_ApplyViewportOffset(ScreenUV, bApplyViewportOffset);
	float4 EncodedVelocity = 0;
	#if NDIGBUFFER_USE_GBUFFER_VELOCITY
		EncodedVelocity = GetGBufferData(ScreenUV).Velocity;
	#else
		EncodedVelocity = NDIGBuffer_VelocityTexture.SampleLevel(NDIGBuffer_VelocityTextureSampler, ScreenUV, 0);
	#endif
	IsValid = true;
	if ( EncodedVelocity.x > 0 )
	{
	#if VELOCITY_ENCODE_DEPTH
		OutValue = DecodeVelocityFromTexture(EncodedVelocity);
	#else
		OutValue.xy = DecodeVelocityFromTexture(EncodedVelocity).xy;
	#endif
	}
#endif
}

void DIGBuffer_DecodeWorldVelocity(in float2 ScreenUV, in bool bApplyViewportOffset, out bool IsValid, out float3 OutValue)
{
	IsValid = false;
	OutValue = 0;
#if NDIGBUFFER_HAS_GBUFFER
	ScreenUV = DIGBuffer_ApplyViewportOffset(ScreenUV, bApplyViewportOffset);
	float4 EncodedVelocity = 0;
	#if NDIGBUFFER_USE_GBUFFER_VELOCITY
		EncodedVelocity = GetGBufferData(ScreenUV).Velocity;
	#else
		EncodedVelocity = NDIGBuffer_VelocityTexture.SampleLevel(NDIGBuffer_VelocityTextureSampler, ScreenUV, 0);
	#endif
	IsValid = true;
	if ( EncodedVelocity.x > 0 )
	{
	#if VELOCITY_ENCODE_DEPTH
		OutValue = DecodeVelocityFromTexture(EncodedVelocity);
		OutValue = mul(float4(OutValue, 0), View.ViewToTranslatedWorld).xyz;
	#else
		float2 ScreenVelocity = DecodeVelocityFromTexture(EncodedVelocity).xy;
		ScreenVelocity.y = -ScreenVelocity.y;

		{
			float2 PrevScreenUV = ScreenUV - ScreenVelocity;

			float SceneDepth = CalcSceneDepth(ScreenUV);
			float2 ScreenPosition = (ScreenUV - View.ScreenPositionScaleBias.wz) / View.ScreenPositionScaleBias.xy;
			float3 WorldPosition = mul(float4(ScreenPosition * SceneDepth, SceneDepth, 1), LWCHackToFloat(PrimaryView.ScreenToWorld)).xyz;

			// Ideally we would use CalcPrevSceneDepth(PrevScreenUV) here to get an accurate location, however we do not store PrevSceneDepth so this is a very rough approximation
			float PrevSceneDepth = SceneDepth;	//CalcPrevSceneDepth(PrevScreenUV);
			float2 PrevScreenPosition = (PrevScreenUV - View.ScreenPositionScaleBias.wz) / View.ScreenPositionScaleBias.xy;
			float3 PrevWorldPosition = mul(float4(PrevScreenPosition * PrevSceneDepth, PrevSceneDepth, 1), LWCHackToFloat(PrimaryView.ScreenToWorld)).xyz;

			OutValue = WorldPosition - PrevWorldPosition;
		}
	#endif
	}
#endif
}

void DIGBuffer_DecodeBaseColor(in float2 ScreenUV, in bool bApplyViewportOffset, out bool IsValid, out float3 OutValue)
{
#if NDIGBUFFER_HAS_GBUFFER
	ScreenUV = DIGBuffer_ApplyViewportOffset(ScreenUV, bApplyViewportOffset);
	FGBufferData GBufferData = GetGBufferData(ScreenUV);
	IsValid = true;
	OutValue = GBufferData.BaseColor;
#else
	IsValid = false;
	OutValue = 0;
#endif
}

void DIGBuffer_DecodeMetallic(in float2 ScreenUV, in bool bApplyViewportOffset, out bool IsValid, out float OutValue)
{
#if NDIGBUFFER_HAS_GBUFFER
	ScreenUV = DIGBuffer_ApplyViewportOffset(ScreenUV, bApplyViewportOffset);
	FGBufferData GBufferData = GetGBufferData(ScreenUV);
	IsValid = true;
	OutValue = GBufferData.Metallic;
#else
	IsValid = false;
	OutValue = 0;
#endif
}

void DIGBuffer_DecodeSpecular(in float2 ScreenUV, in bool bApplyViewportOffset, out bool IsValid, out float OutValue)
{
#if NDIGBUFFER_HAS_GBUFFER
	ScreenUV = DIGBuffer_ApplyViewportOffset(ScreenUV, bApplyViewportOffset);
	FGBufferData GBufferData = GetGBufferData(ScreenUV);
	IsValid = true;
	OutValue = GBufferData.Specular;
#else
	IsValid = false;
	OutValue = 0;
#endif
}

void DIGBuffer_DecodeRoughness(in float2 ScreenUV, in bool bApplyViewportOffset, out bool IsValid, out float OutValue)
{
#if NDIGBUFFER_HAS_GBUFFER
	ScreenUV = DIGBuffer_ApplyViewportOffset(ScreenUV, bApplyViewportOffset);
	FGBufferData GBufferData = GetGBufferData(ScreenUV);
	IsValid = true;
	OutValue = GBufferData.Roughness;
#else
	IsValid = false;
	OutValue = 0;
#endif
}

void DIGBuffer_DecodeDepth(in float2 ScreenUV, in bool bApplyViewportOffset, out bool IsValid, out float OutValue)
{
#if NDIGBUFFER_HAS_GBUFFER
	ScreenUV = DIGBuffer_ApplyViewportOffset(ScreenUV, bApplyViewportOffset);
	FGBufferData GBufferData = GetGBufferData(ScreenUV);
	IsValid = true;
	OutValue = GBufferData.Depth;
#elif NDIGBUFFER_IS_MOBILE
	IsValid = true;
	OutValue = CalcSceneDepth(ScreenUV);
#else
	IsValid = false;
	OutValue = 0;
#endif
}

#if 0
// This has not been tested yet
void DIGBuffer_DecodeStencil(in float2 ScreenUV, in bool bApplyViewportOffset, out bool IsValid, out int OutValue)
{
#if NDIGBUFFER_HAS_GBUFFER
	IsValid = true;

	ScreenUV = DIGBuffer_ApplyViewportOffset(ScreenUV, bApplyViewportOffset);
	uint2 IntUV = (uint2)(ScreenUV.xy * (float2)View.BufferSizeAndInvSize.xy);
	uint Stencil = SceneStencilTexture.Load(uint3(IntUV, 0)) STENCIL_COMPONENT_SWIZZLE;
	OutValue = int(Stencil);
#else
	IsValid = false;
	OutValue = 0;
#endif
}
#endif

void DIGBuffer_DecodeCustomDepth(in float2 ScreenUV, in bool bApplyViewportOffset, out bool IsValid, out float OutValue)
{
#if NDIGBUFFER_HAS_GBUFFER
	ScreenUV = DIGBuffer_ApplyViewportOffset(ScreenUV, bApplyViewportOffset);
	FGBufferData GBufferData = GetGBufferData(ScreenUV);
	IsValid = true;
	OutValue = GBufferData.CustomDepth;
#elif NDIGBUFFER_IS_MOBILE
	IsValid = true;
	OutValue = ConvertFromDeviceZ(Texture2DSampleLevel(MobileSceneTextures.CustomDepthTexture, MobileSceneTextures.CustomDepthTextureSampler, ScreenUV, 0).r);
#else
	IsValid = false;
	OutValue = 0;
#endif
}

void DIGBuffer_DecodeCustomStencil(in float2 ScreenUV, in bool bApplyViewportOffset, out bool IsValid, out int OutValue)
{
#if NDIGBUFFER_HAS_GBUFFER
	ScreenUV = DIGBuffer_ApplyViewportOffset(ScreenUV, bApplyViewportOffset);
	FGBufferData GBufferData = GetGBufferData(ScreenUV);
	IsValid = true;
	OutValue = GBufferData.CustomStencil;
#elif NDIGBUFFER_IS_MOBILE
	IsValid = true;
	OutValue = Texture2DSampleLevel(MobileSceneTextures.MobileCustomStencilTexture, MobileSceneTextures.MobileCustomStencilTextureSampler, ScreenUV, 0).r * 255.0f;
#else
	IsValid = false;
	OutValue = 0;
#endif
}

void DIGBuffer_DecodeSceneColor(in float2 ScreenUV, in bool bApplyViewportOffset, out bool IsValid, out float4 OutValue)
{
#if NDIGBUFFER_HAS_GBUFFER
	IsValid = true;
	ScreenUV = DIGBuffer_ApplyViewportOffset(ScreenUV, bApplyViewportOffset);
	OutValue = Texture2DSampleLevel(SceneTexturesStruct_SceneColorTexture, SceneTexturesStruct_SceneColorTextureSampler, ScreenUV, 0);
	OutValue.rgb *= View.OneOverPreExposure.xxx;
#elif NDIGBUFFER_IS_MOBILE
	IsValid = true;
	OutValue = Texture2DSampleLevel(MobileSceneTextures.SceneColorTexture, MobileSceneTextures.SceneColorTextureSampler, ScreenUV, 0);
	OutValue.rgb *= View.OneOverPreExposure.xxx;
#else
	IsValid = false;
	OutValue = 0;
#endif
}

void DIGBuffer_DecodeShadingModelID(in float2 ScreenUV, in bool bApplyViewportOffset, out bool IsValid, out int OutValue)
{
#if NDIGBUFFER_HAS_GBUFFER
	ScreenUV = DIGBuffer_ApplyViewportOffset(ScreenUV, bApplyViewportOffset);
	FGBufferData GBufferData = GetGBufferData(ScreenUV);
	IsValid = true;
	OutValue = GBufferData.ShadingModelID;
#else
	IsValid = false;
	OutValue = 0;
#endif
}
