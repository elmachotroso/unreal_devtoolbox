// Copyright Epic Games, Inc. All Rights Reserved.
// RWInstanceCounts come from NiagaraEmitterInstanceSimulation.ush as we use counts from that buffer
// VariableType		- Variable type, i.e. float, float2, etc
// RWBufferType		- RW Buffer type, generally float or int since we don't support typed UAV loads across all platforms
// RWBufferRead		- Hlsl code to read from the buffer, i.e Value = BUFFER_NAME[Index]
// RWBufferWrite	- Hlsl code to write to the buffer, i.e BUFFER_NAME[Index] = Value

RWBuffer<{RWBufferType}> ArrayBuffer_{ParameterName};
int2 ArrayBufferParams_{ParameterName};				// Where x=CountOffset & y=BufferCapacity

int GetCountOffset_{ParameterName}() { return ArrayBufferParams_{ParameterName}.x; }
int GetCapacity_{ParameterName}() { return ArrayBufferParams_{ParameterName}.y; }
int GetLength_{ParameterName}() { return RWInstanceCounts[GetCountOffset_{ParameterName}()]; }

void Length_{ParameterName}(out int ArrayLength)
{
	ArrayLength = GetLength_{ParameterName}();
}

void IsValidIndex_{ParameterName}(int Index, out bool bIsValid)
{
	bIsValid = Index >= 0 && Index < GetLength_{ParameterName}();
}

void LastIndex_{ParameterName}(out int Index)
{
	Index = GetLength_{ParameterName}() - 1;
}

void Get_{ParameterName}(int Index, out {VariableType} Value)
{
	int Length = GetLength_{ParameterName}();
	Index = Length > 0 ? clamp(Index, 0, Length - 1) : GetCapacity_{ParameterName}();
	#define BUFFER_NAME ArrayBuffer_{ParameterName}
	{RWBufferRead}
	#undef BUFFER_NAME
}

void SetArrayElem_{ParameterName}_UEImpureCall(bool bSkip, int Index, {VariableType} Value)
{
	bool bIsValidIndex;
	IsValidIndex_{ParameterName}(Index, bIsValidIndex);
	if ( !bSkip && bIsValidIndex )
	{
		#define BUFFER_NAME ArrayBuffer_{ParameterName}
		{RWBufferWrite}
		#undef BUFFER_NAME
	}
}

void Add_{ParameterName}_UEImpureCall(bool bSkip, {VariableType} Value)
{
	if ( bSkip == true )
	{
		return;
	}

	uint Index;
	InterlockedAdd(RWInstanceCounts[GetCountOffset_{ParameterName}()], 1, Index);
	if ( Index < GetCapacity_{ParameterName}() )
	{
		#define BUFFER_NAME ArrayBuffer_{ParameterName}
		{RWBufferWrite}
		#undef BUFFER_NAME
	}
	else
	{
		InterlockedAdd(RWInstanceCounts[GetCountOffset_{ParameterName}()], uint(-1));
	}
}

void RemoveLastElem_{ParameterName}_UEImpureCall(bool bSkip, out {VariableType} Value, out bool bOutValid)
{
	uint Index;
	bOutValid = false;

	if ( bSkip == false )
	{
		InterlockedAdd(RWInstanceCounts[GetCountOffset_{ParameterName}()], uint(-1), Index);

		bOutValid = Index < GetCapacity_{ParameterName}();
		if ( !bOutValid )
		{
			InterlockedAdd(RWInstanceCounts[GetCountOffset_{ParameterName}()], 1);
		}
	}

	Index = bOutValid ? Index : GetCapacity_{ParameterName}();
	#define BUFFER_NAME ArrayBuffer_{ParameterName}
	{RWBufferRead}
	#undef BUFFER_NAME
}
